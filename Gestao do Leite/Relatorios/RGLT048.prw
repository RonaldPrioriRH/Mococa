#INCLUDE "REPORT.CH"
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณ RGLT048  บ Autor ณ TOTVS            	    บ Data da Criacao  ณ 24/08/2010                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณ Relacao de Notas Fiscais emitidas.                                                                 			บฑฑ
ฑฑบ          ณ 															                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Tem por objetivo: 																							บฑฑ
ฑฑบ			 ณ			*Imprimir o a relacao de Notas Fiscais emitidas dos produtores, oriundas do fechamento do leite.    บฑฑ
ฑฑบ			 ณ                                           	                                     							บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ Nenhum.                                                                                 						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nenhum                                                 	                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ                                                                                          					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Leite                                                                                 						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
User Function RGLT048

Private oReport
Private oSF1
Private QRYSF1 := ""
Private aOrd   := {"Nome","Codigo+Loja","Nro NF+Serie"}
Private cPerg  := PADR("RGLT048",10)

ValidPerg()
Pergunte(cPerg,.F.)

DEFINE REPORT oReport NAME "RGLT048" TITLE "Rela็ใo de Notas Fiscais Emitidas" PARAMETER cPerg ACTION {|oReport| PrintReport(oReport)}

//Seta Padrao de impressao Paisagem.
//oReport:SetLandscape()
oReport:SetTotalInLine(.F.)

//Secao Dados
DEFINE SECTION oSF1 OF oReport  TITLE "Dados" TABLES "SF1"  ORDERS aOrd
DEFINE CELL NAME "F1_FORNECE" OF oSF1 ALIAS "SF1" TITLE "Cod."       SIZE 6
DEFINE CELL NAME "F1_LOJA"    OF oSF1 ALIAS "SF1" TITLE "Loja"       SIZE 2
DEFINE CELL NAME "A2_NOME"	  OF oSF1 ALIAS "SA2" TITLE "Nome"       PICTURE "@S30" SIZE 40
DEFINE CELL NAME "F1_DOC"	  OF oSF1 ALIAS "SF1" TITLE "Num. NF"    SIZE 12
DEFINE CELL NAME "F1_SERIE"	  OF oSF1 ALIAS "SF1" TITLE "Serie"      SIZE 3
DEFINE CELL NAME "TOTALLTE"   OF oSF1 ALIAS ""    TITLE "Volume"     PICTURE "@E 99,999,999"     SIZE 10 BLOCK {|| GetTotLte(QRYSF1->F1_FORNECE,QRYSF1->F1_LOJA)}
DEFINE CELL NAME "MATGORDA"   OF oSF1 ALIAS ""    TITLE "M. Gor"     PICTURE "@E 999,999.99"     SIZE 12 BLOCK {|| GetMatGor(QRYSF1->F1_FORNECE,QRYSF1->F1_LOJA)}
DEFINE CELL NAME "BONIFICA"   OF oSF1 ALIAS ""    TITLE "Total Bon." PICTURE "@E 999,999,999.99" SIZE 16 BLOCK {|| GetBon(QRYSF1->F1_FORNECE,QRYSF1->F1_LOJA)}
DEFINE CELL NAME "D1_TOTAL"	  OF oSF1 ALIAS "SD1" TITLE "Total NF"   PICTURE "@E 999,999,999.99" SIZE 16
DEFINE CELL NAME "FUNRURAL"   OF oSF1 ALIAS ""    TITLE "FunRural"   PICTURE "@E 999,999,999.99" SIZE 16  BLOCK {|| GetFunRural(QRYSF1->F1_FORNECE,QRYSF1->F1_LOJA)}
DEFINE CELL NAME "LIQUIDO"    OF oSF1 ALIAS ""    TITLE "Liquido"    PICTURE "@E 999,999,999.99" SIZE 16  BLOCK {|| QRYSF1->D1_TOTAL - GetFunRural(QRYSF1->F1_FORNECE,QRYSF1->F1_LOJA) }

DEFINE FUNCTION FROM oSF1:Cell("TOTALLTE")  OF oSF1 FUNCTION SUM NO END SECTION
DEFINE FUNCTION FROM oSF1:Cell("MATGORDA")  OF oSF1 FUNCTION SUM NO END SECTION
DEFINE FUNCTION FROM oSF1:Cell("BONIFICA")  OF oSF1 FUNCTION SUM NO END SECTION
DEFINE FUNCTION FROM oSF1:Cell("D1_TOTAL")  OF oSF1 FUNCTION SUM NO END SECTION
DEFINE FUNCTION FROM oSF1:Cell("FUNRURAL")  OF oSF1 FUNCTION SUM NO END SECTION
DEFINE FUNCTION FROM oSF1:Cell("LIQUIDO")   OF oSF1 FUNCTION SUM NO END SECTION

oSF1:Cell("TOTALLTE"):SetHeaderAlign("RIGHT")
oSF1:Cell("MATGORDA"):SetHeaderAlign("RIGHT")
oSF1:Cell("BONIFICA"):SetHeaderAlign("RIGHT")
oSF1:Cell("FUNRURAL"):SetHeaderAlign("RIGHT")
oSF1:Cell("LIQUIDO"):SetHeaderAlign("RIGHT")

oReport:PrintDialog()
Return()

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ PrintReport บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Imprime relatorio.                                            บฑฑ
ฑฑบ          ณ                                                               บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PrintReport(oReport)

Local cFiltro := "%"

cFiltro += " AND SF1.F1_FORNECE BETWEEN '" + MV_PAR01 + "' AND '" + MV_PAR02 + "' "
cFiltro += " AND SF1.F1_LOJA BETWEEN    '" + MV_PAR03 + "' AND '" + MV_PAR04 + "' "
cFiltro += " AND SF1.F1_EMISSAO BETWEEN '" + DTOS(MV_PAR05) + "' AND '" + DTOS(MV_PAR06) + "' "

//Filtra Filial
If !Empty(MV_PAR07)
	cFiltro += " AND SF1.F1_FILIAL = '" + MV_PAR07 + "' "
EndIf

If MV_PAR12 == 1 //Ativos ou Nao Bloqueados
	cFiltro += " AND SA2.A2_MSBLQL = '2' "
ElseIf MV_PAR12 == 2//Inativos ou Bloqueados
	cFiltro += " AND SA2.A2_MSBLQL = '1' "
EndIf

cFiltro += "%"
nOrdem := oSF1:GetOrder()

If nOrdem == 1
	cOrdem := "% SA2.A2_NOME %"
ElseIf nOrdem == 2
	cOrdem := "% SA2.A2_COD,SA2.A2_LOJA %"
Else
	cOrdem := "% SF1.F1_DOC,SF1.F1_SERIE %"
EndIf

//Define query para ordem Supervisor
BEGIN REPORT QUERY oSF1
BeginSql alias "QRYSF1"
	SELECT
	SF1.F1_FORNECE,SF1.F1_LOJA,SA2.A2_NOME,SF1.F1_DOC,SF1.F1_SERIE,SF1.F1_EMISSAO,SA2.A2_CGC,SA2.A2_INSCR,
	SF1.F1_FILIAL,SA2.A2_CEP,SUM(SD1.D1_TOTAL) AS D1_TOTAL
	FROM
	%table:SF1% SF1
	JOIN %table:SA2% SA2 ON SF1.F1_FORNECE = SA2.A2_COD AND SF1.F1_LOJA = SA2.A2_LOJA AND SA2.%notDel%
	JOIN %table:SD1% SD1 ON SD1.D1_DOC = SF1.F1_DOC AND SD1.D1_SERIE = SF1.F1_SERIE AND SD1.D1_FILIAL = SF1.F1_FILIAL AND SD1.%notDel%
	WHERE
	SF1.%notDel%
	AND SF1.F1_TIPO = 'N'
	AND SA2.%notDel% AND SA2.A2_L_TPFOR = 'P'
	%Exp:cFiltro%
	GROUP BY                                                       `
	SF1.F1_FORNECE,SF1.F1_LOJA,SA2.A2_NOME,SF1.F1_DOC,SF1.F1_SERIE,SF1.F1_EMISSAO,SA2.A2_CGC,SA2.A2_INSCR,
	SF1.F1_FILIAL,SA2.A2_CEP
	ORDER BY
	%exp:cOrdem%
EndSql
END REPORT QUERY oSF1

oSF1:Print(.T.)
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GetMatGor   บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna o total de materia gorda do produtor.                 บฑฑ
ฑฑบ          ณ                                                               บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GetMatGor(cFornece,cLoja)

Local aArea     := GetArea()
Local nRet      := 0
Local cChaveZLF := xFILIAL("ZLF")+MV_PAR08+MV_PAR09+cFornece+cLoja+MV_PAR10

dbSelectArea("ZLF")
dbSetOrder(1)
dbSeek(cChaveZLF)

While ZLF->(!Eof()) .And. ZLF->(ZLF_FILIAL+ZLF_CODZLE+ZLF_VERSAO+ZLF_A2COD+ZLF_A2LOJA+ZLF_EVENTO) == cChaveZLF
	nRet += ZLF->ZLF_TOTAL
	ZLF->(dbSkip())
EndDo

RestArea(aArea)
Return(nRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GetFunRural บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna o total de funrural do produtor.                      บฑฑ
ฑฑบ          ณ                                                               บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GetFunRural(cFornece,cLoja)

Local aArea     := GetArea()
Local nRet      := 0
Local cChaveZLF := xFILIAL("ZLF")+MV_PAR08+MV_PAR09+cFornece+cLoja+MV_PAR11

dbSelectArea("ZLF")
dbSetOrder(1)
dbSeek(cChaveZLF)

While ZLF->(!Eof()) .And. ZLF->(ZLF_FILIAL+ZLF_CODZLE+ZLF_VERSAO+ZLF_A2COD+ZLF_A2LOJA+ZLF_EVENTO) == cChaveZLF
	nRet += ZLF->ZLF_TOTAL
	ZLF->(dbSkip())
EndDo

RestArea(aArea)
Return(nRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GetBon      บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna o total de bonificacao do produtor.                   บฑฑ
ฑฑบ          ณ                                                               บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GetBon(cFornece,cLoja)

Local aArea    := GetArea()
Local aAreaZL8 := ZL8->(GetArea())
Local nRet     := 0
Local nI       := 0
Local cGrpBon  := ALLTRIM(GetMv("LT_GRPBON"))
Local aEventos := {}
Local cChaveZLF:= ""

//Busca todos eventos de bonificacao
dbSelectArea("ZL8")
dbSetOrder(3)
dbSeek(xFILIAL("ZL8")+cGrpBon)

While ZL8->(!Eof()) .And. ZL8->ZL8_FILIAL == xFILIAL("ZL8") .And. alltrim(ZL8->ZL8_GRUPO) == alltrim(cGrpBon)
	//Verifica para nao incluir materia gorda
	If ZL8->ZL8_COD != MV_PAR10
		aadd(aEventos,ZL8->ZL8_COD)
	EndIf
	ZL8->(dbSkip())
EndDo

For nI := 1 To Len(aEventos)
	cChaveZLF := xFILIAL("ZLF")+MV_PAR08+MV_PAR09+cFornece+cLoja+aEventos[nI]

    dbSelectArea("ZLF")
    dbSetOrder(1)	
	dbSeek(cChaveZLF)
	
	While ZLF->(!Eof()) .And. ZLF->(ZLF_FILIAL+ZLF_CODZLE+ZLF_VERSAO+ZLF_A2COD+ZLF_A2LOJA+ZLF_EVENTO) == cChaveZLF
		If ZLF->ZLF_DEBCRE == "D"
			nRet -= ZLF->ZLF_TOTAL
		Else
			nRet += ZLF->ZLF_TOTAL
		EndIf
		ZLF->(dbSkip())
	EndDo
Next nI

ZL8->(RestArea(aAreaZL8))
RestArea(aArea)
Return(nRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GetTotLte   บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna o total de leite entregue pelo produtor.              บฑฑ
ฑฑบ          ณ                                                               บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GetTotLte(cFornece,cLoja)

Local aArea     := GetArea()
Local nRet      := 0
Local cEvento   := ALLTRIM(GetMv("LT_EVELTE")) //Evento Leite Cru
Local cChaveZLF := xFILIAL("ZLF")+MV_PAR08+MV_PAR09+cFornece+cLoja+cEvento

dbSelectArea("ZLF")
dbSetOrder(1)
dbSeek(cChaveZLF)

While ZLF->(!Eof()) .And. ZLF->(ZLF_FILIAL+ZLF_CODZLE+ZLF_VERSAO+ZLF_A2COD+ZLF_A2LOJA+ZLF_EVENTO) == cChaveZLF
	nRet += ZLF->ZLF_QTDBOM
	ZLF->(dbSkip())
EndDo

RestArea(aArea)
Return nRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ ValidPerg   บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Cria as perguntas, caso elas nao existam.                     บฑฑ
ฑฑบ          ณ                                                               บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ValidPerg()

Local aHelpPor := {}
Local aHelpSpa := {}
Local aHelpEng := {}

//Do Produtor
aHelpPor := {}
Aadd( aHelpPor, 'Informe o c๓digo inicial do Produtor')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"01","Do Produtor"," "," ","mv_ch1","C",6,0,0,"G","","FOR","","","MV_PAR01","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Ate Produtor
aHelpPor := {}
Aadd( aHelpPor, 'Informe o c๓digo final do Produtor')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"02","Ate Produtor"," "," ","mv_ch2","C",6,0,0,"G","","FOR","","","MV_PAR02","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Loja
aHelpPor := {}
Aadd( aHelpPor, 'Informe loja inicial do Produtor  ')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"03","Da Loja "," "," ","mv_ch3","C",2,0,0,"G","","","","","MV_PAR03","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Loja
aHelpPor := {}
Aadd( aHelpPor, 'Informe loja final do Produtor  ')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"04","Ate Loja "," "," ","mv_ch4","C",2,0,0,"G","","","","","MV_PAR04","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Da Data
Aadd( aHelpPor, 'Informe a data inicial')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"05","De Data"," "," ","mv_ch5","D",8,0,0,"G","","","","","MV_PAR05","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Ate Dia
aHelpPor := {}
Aadd( aHelpPor, 'Informe a data final que deseja filtrar')
U_xPutSx1(cPerg,"06","Ate Data"," "," ","mv_ch6","D",8,0,0,"G","","","","","MV_PAR06","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Filial
aHelpPor := {}
Aadd( aHelpPor, 'Informe a filial')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"07","Filial"," "," ","mv_ch7","C",4,0,0,"G","","SM0","","","MV_PAR07","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Do Mix
aHelpPor := {}
Aadd( aHelpPor, 'Codigo do Mix a ser considerado     ')
U_xPutSx1(cPerg,"08","Do Mix"," "," ","mv_ch8","C",6,0,0,"G","","ZLE_01","","","MV_PAR08","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Versao
aHelpPor := {}
Aadd( aHelpPor, 'Informe a Versao do Mix  ')
Aadd( aHelpPor, 'que deseja filtrar.                 ')
U_xPutSx1(cPerg,"09","Versao  "," "," ","mv_ch9","C",1,0,0,"G","","","","","MV_PAR09","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Evento Mat. Gorda
aHelpPor := {}
Aadd( aHelpPor, 'Informa o codigo do evento para buscar valor')
Aadd( aHelpPor, 'do materia gorda.                           ')
U_xPutSx1(cPerg,"10","Evento M.Gor"," "," ","mv_cha","C",6,0,0,"G","","ZL8_01","","","MV_PAR10","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Evento FunRural
aHelpPor := {}
Aadd( aHelpPor, 'Informa o codigo do evento para buscar valor')
Aadd( aHelpPor, 'do FunRural.                                ')
U_xPutSx1(cPerg,"11","Evento FunRural"," "," ","mv_chb","C",6,0,0,"G","","ZL8_01","","","MV_PAR11","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

//Tipo Produtores - Ativos/Inativos/Ambos
aHelpPor := {}
Aadd( aHelpPor, 'Informe os produtores que deseja filtrar')
U_xPutSx1(cPerg,"12","Status"," "," ","mv_chc","C",1,0,0,"C","","","","","MV_PAR12","Ativos","","","","Inativos","","","Ambos","Ambos","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

Return()