#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณ MGLT016  บ Autor ณ TOTVS                 บ Data da Criacao  ณ 12/02/2009                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณ Rotina desenvolvida para possibilitar o cancelamento dos cheques dos produtores.                             บฑฑ
ฑฑบ          ณ                        									                               				        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Cancelamento dos cheques dos produtores automaticamente.                                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ Nenhum						   							                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nenhum						  							                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite.                                                                        						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ          ณ                                                  ณ                                  ณ   	        บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
User Function MGLT016

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Declaracao de Variaveis.             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Private cMarca    := GetMark()
Private lInverte  := .F.
Private lDeuErro  := .F.
Private cPerg     := "MGLT016"
Private cCadastro := "Cancelamento de Cheque do Produtor/Fretista"
Private aCampos   := {}
Private aRotina   := {}
Private bCancCh   := {|| CancChq(cMarca)}
Private cBanco    := ""
Private cAgencia  := ""
Private cConta    := ""
Private aTitulos  := {}
Private aProdutor := {}

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Atribuicao de Valores no aRotina.    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Private aRotina:={{"Cancela Chq",'Eval(bCancCh)',0,2,0}}

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Chama funcao para criar os parametros caso nao existam.             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
AjustaSX1()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Chama a tela para preenchimento dos parametros.                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Pergunte(cPerg,.T.)
	Return()
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Atualiza os valores das variaveis.                                  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cBanco   := MV_PAR01
cAgencia := MV_PAR02
cConta   := MV_PAR03

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Cria o arquivo Temporario para insercao dos dados selecionados. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
MsgRun("Aguarde.... Criando arquivo temporario...",,{||CursorWait(), GLTCRIA(), CursorArrow()})

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Chama funcao para selecao dos dados.                                ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
MsgRun("Aguarde.... Selecionando dados...",,{||CursorWait(), GLTSEL(), CursorArrow()})

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Chama funcao para criar a MarkBrowse.                               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
GLTMOST()

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GLTMOST  บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Define os campos a serem apresentados na MarkBrowse.       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GLTMost()

aadd(acampos ,{"TRB_OK"     ,"  "              ,"   "})
aadd(acampos ,{"TRB_FORNEC" ,"@!!!!!!"         ,"Prod/Fret"})
aadd(acampos ,{"TRB_LOJA"   ,"@!!!!"           ,"Loja"})
aadd(acampos ,{"TRB_NOME"   ,"@!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!","Nome"})
aadd(acampos ,{"TRB_CHEQUE" ,"@!!!!!!!!!!!!!!!","Cheque"})
aadd(acampos ,{"TRB_VALOR"  ,"","Valor",PesqPict("SE2","E2_VALOR",14,2) })
aadd(acampos ,{"TRB_IMPRES" ,"@!!!"            ,"Impresso"})
aadd(acampos ,{"TRB_BANCO"  ,"@!!!"            ,"Banco"})
aadd(acampos ,{"TRB_AGENCI" ,"@!!!!!"          ,"Agencia"})
aadd(acampos ,{"TRB_CONTA"  ,"@!!!!!!!!!!"     ,"Conta"})
aadd(acampos ,{"TRB_SETOR"  ,"@!!!!!!"         ,"Setor"})
aadd(acampos ,{"TRB_LINHA"  ,"@!!!!!!"         ,"Linha"})
aadd(acampos ,{"TRB_MIX"    ,"@!!!!!!"         ,"Mix"})
aadd(acampos ,{"TRB_VERSAO" ,"@!"              ,"Versao"})

dbSelectArea("TRB")
TRB->(DbGotop())

MarkBrow("TRB","TRB_OK",,aCampos,.F.,cMarca,,,,,)

dbSelectArea("TRB")
DbCloseArea()

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ CancChq  บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Gera o cheque do produtor.                                 บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CancChq()

Local nProc1   := 0
Local nProc2   := 0

If MsgYesNo("Deseja cancelar CHEQUE para os produtores/fretistas marcados?")
	
	DbSelectArea("TRB")
	TRB->(DbGoTop())
	
	While TRB->(!Eof())
		
		If IsMark( 'TRB_OK', cMarca )
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Grava no array aProdutor que o cheque deve ser cancelado.  ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			nAcho := Ascan(aProdutor,{|x| x[1] == ALLTRIM(TRB->TRB_CHEQUE) })
			If nAcho > 0
				aProdutor[nAcho][8] := .T. // .T. cancela o cheque e a baixa do titulo.
			EndIf
		EndIf
		
		TRB->(DbSkip())
	EndDo

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Cancela o cheque para cada produtor marcado no arquivo TRB. ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	For nProc1 := 1 To Len(aProdutor)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณPosiciona no cadastro do Fornecedor.                        ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		DbSelectArea("SA2")
		DbSetOrder(1)//A2_FILIAL+A2_COD+A2_LOJA
		DbSeek(xFILIAL("SA2")+aProdutor[nProc1][6]+aProdutor[nProc1][7])
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Inicia o controle de transacao. ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Begin Transaction
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Chama funcao para fazer o cancelamento do Cheque antes de cancelar a baixa do Titulo. ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If aProdutor[nProc1][8]
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณExecuta a rotina de cancelamento de cheque.                 ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			MsgRun("Cancelando o cheque "+aProdutor[nProc1][3],,{||CursorWait(),;
			lDeuErro := CancCheq(aProdutor[nProc1][3],aProdutor[nProc1][4],aProdutor[nProc1][5],PADR(aProdutor[nProc1][1],TamSX3("EF_NUM")[1]) ), CursorArrow()})
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Processa todos os titulos, do produtor.                                                         ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			For nProc2 := 1 To Len(aTitulos)
				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณ Se cheque do Produtor = cheque do Titulo e cancela cheque = .T.                                 ณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				If aProdutor[nProc1][1] == aTitulos[nProc2][1] .And. aProdutor[nProc1][8] .And. !lDeuErro
					//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
					//ณExecuta o SigaAuto de cancelamento de Baixa de Titulo.      ณ
					//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
					MsgRun("Cancelando a baixa do titulo de "+aTitulos[nProc2][9],,{||CursorWait(),;
					CancBxSE2(aTitulos[nProc2][2],aTitulos[nProc2][3],aTitulos[nProc2][4],aTitulos[nProc2][5],aTitulos[nProc2][6],;
					aTitulos[nProc2][7],aTitulos[nProc2][8]), CursorArrow()})
				EndIf
			Next nProc2
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณDeleta os cheques que foram selecionados.                   ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !lDeuErro .And. aProdutor[nProc1][8]
			DbSelectArea("TRB")
			TRB->(DbGoTo(aProdutor[nProc1][9]))
			RecLock( 'TRB', .F. )
			DbDelete()
			MsUnLock()
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Se houve alguma falha, desfaz todas as transacoes.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If lDeuErro
			DisarmTransaction()
			lDeuErro := .F.
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Finaliza o controle de transacao, caso nao hajam falhas. ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		End Transaction
	Next nProc1
EndIf

Return()

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GLTCRIA  บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Cria um arquivo Temporario com os campos a serem apresen-  บฑฑ
ฑฑบ          ณ tados na MarkBrowse.                                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GLTCria()

Local aEstru := {}
Local aArq2

aadd(aEstru,{"TRB_OK"      ,'C',02,0})
aadd(aEstru,{"TRB_FORNEC"  ,'C',06,0})
aadd(aEstru,{"TRB_LOJA"    ,'C',04,0})
aadd(aEstru,{"TRB_NOME"    ,'C',40,0})
aadd(aEstru,{"TRB_CHEQUE"  ,'C',15,0})
aadd(aEstru,{"TRB_VALOR"   ,'N',14,2})
aadd(aEstru,{"TRB_IMPRES"  ,'C',01,0})
aadd(aEstru,{"TRB_BANCO"   ,'C',03,0})
aadd(aEstru,{"TRB_AGENCI"  ,'C',05,0})
aadd(aEstru,{"TRB_CONTA"   ,'C',10,0})
aadd(aEstru,{"TRB_SETOR"   ,'C',06,0})
aadd(aEstru,{"TRB_LINHA"   ,'C',06,0})
aadd(aEstru,{"TRB_MIX"     ,'C',06,0})
aadd(aEstru,{"TRB_VERSAO"  ,'C',01,0})

aArq2 := CriaTrab(aEstru,.T.)
If Select("TRB") <> 0
	TRB->(dbCloseArea())
EndIf
Use &aArq2 Alias TRB NEW
Index on TRB_CHEQUE To &aArq2

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GLTSEL   บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Seleciona os dados a serem apresentados na MarkBrowse.     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa Principal.                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function GLTSel()

Local cQuery  := ""
Local cPrdFab := ""
Local nTabFor := 0
Local nVlrIPI := 0

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Query para Selecao dos dados.                                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cQuery := "SELECT E2_BCOPAG,E2_NUMBCO,E2_IMPCHEQ,E2_PREFIXO,E2_NUM,E2_PARCELA,E2_TIPO,E2_FORNECE,E2_LOJA,"
cQuery += "E2_VALOR,E2_SALDO,E2_L_LINRO,E2_L_SETOR,E2_L_MIX,E2_L_VERSA,"
cQuery += "E5_VALOR,E5_SEQ,E5_MOTBX,E5_DATA,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_NUMCHEQ FROM "
cQuery += RetSqlName("SE2")+" SE2, "+RetSqlName("SE5")+" SE5"
cQuery += " WHERE SE2.D_E_L_E_T_ = ' ' AND SE5.D_E_L_E_T_ = ' ' "
cQuery += "AND SE2.E2_FILIAL  = '"+xFILIAL("SE2")+"' "
cQuery += "AND SE5.E5_FILIAL  = '"+xFILIAL("SE5")+"' "
cQuery += "AND SE2.E2_L_MIX   = '"+MV_PAR04+"' "
cQuery += "AND SE2.E2_L_VERSA BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"' "
cQuery += "AND SE2.E2_FORNECE BETWEEN '"+MV_PAR08+"' AND '"+MV_PAR09+"' "
cQuery += "AND SE2.E2_LOJA    BETWEEN '"+MV_PAR10+"' AND '"+MV_PAR11+"' "
cQuery += "AND SE2.E2_L_LINRO BETWEEN '"+MV_PAR12+"' AND '"+MV_PAR13+"' "
If !Empty(Alltrim(MV_PAR07))    //Se o parametro com os setores estiver vazio considera todos.
	cQuery += " AND SE2.E2_L_SETOR IN (" + U_AjustSet(Alltrim(MV_PAR07)) + ") "
EndIf
cQuery += "AND SE2.E2_TIPO  = 'NF ' "   //Somente titulos a pagar para o Produtor
cQuery += "AND SE2.E2_SALDO = 0 "       //Somente titulos baixados totalmente
cQuery += "AND SE2.E2_PREFIXO = SE5.E5_PREFIXO "
cQuery += "AND SE2.E2_NUM     = SE5.E5_NUMERO "
cQuery += "AND SE2.E2_PARCELA = SE5.E5_PARCELA "
cQuery += "AND SE2.E2_TIPO    = SE5.E5_TIPO "
cQuery += "AND SE2.E2_FORNECE = SE5.E5_FORNECE "
cQuery += "AND SE2.E2_LOJA    = SE5.E5_LOJA "
cQuery += "AND SE5.E5_BANCO   = '"+MV_PAR01+"' "
cQuery += "AND SE5.E5_AGENCIA = '"+MV_PAR02+"' "
cQuery += "AND SE5.E5_CONTA   = '"+MV_PAR03+"' "
cQuery += "AND SE5.E5_NUMCHEQ <> ' ' "
cQuery += "AND SE5.E5_SITUACA <> 'C' "
cQuery += "AND SE5.E5_RECPAG  = 'P' "
cQuery += "AND SE5.E5_TIPODOC = 'BA' "
cQuery += "AND SE5.E5_MOTBX   = 'NOR' "
TCQUERY cQuery NEW ALIAS "TMP"

DbSelectArea("TMP")
TMP->(DbGoTop())

While TMP->(!EOF())
	
	DbSelectArea("TRB")
	TRB->(DbGoTop())
	
	DbSelectArea("SA2")
	DbSetOrder(1)
	DbSeek(xFILIAL("SA2")+TMP->E2_FORNECE+TMP->E2_LOJA)
	
	nAcho := Ascan(aProdutor,{|x| x[1] == ALLTRIM(TMP->E5_NUMCHEQ) })
	If nAcho == 0
		RecLock("TRB",.T.)
		TRB->TRB_FORNEC  := TMP->E2_FORNECE
		TRB->TRB_LOJA    := TMP->E2_LOJA
		TRB->TRB_NOME    := ALLTRIM(SA2->A2_NOME)
		TRB->TRB_CHEQUE  := TMP->E5_NUMCHEQ
		TRB->TRB_VALOR   := TMP->E5_VALOR
		TRB->TRB_IMPRES  := If(SubStr(TMP->E5_NUMCHEQ,1,1)=="*","N","S")
		TRB->TRB_BANCO   := TMP->E5_BANCO
		TRB->TRB_AGENCI  := TMP->E5_AGENCIA
		TRB->TRB_CONTA   := TMP->E5_CONTA
		TRB->TRB_SETOR   := TMP->E2_L_SETOR
		TRB->TRB_LINHA   := TMP->E2_L_LINRO
		TRB->TRB_MIX     := TMP->E2_L_MIX
		TRB->TRB_VERSAO  := TMP->E2_L_VERSA
		MsUnlock("TRB")
		
		AAdd(aProdutor,{ALLTRIM(TMP->E5_NUMCHEQ),TMP->E5_VALOR,TMP->E5_BANCO,TMP->E5_AGENCIA,TMP->E5_CONTA,;
		TMP->E2_FORNECE,TMP->E2_LOJA,.F.,TRB->(Recno())})
	Else
		aProdutor[nAcho][2] += TMP->E5_VALOR
	EndIf
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Armazena os titulos do cheque, para depois cancelar as suas baixas. ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	AAdd(aTitulos,{ALLTRIM(TMP->E5_NUMCHEQ),TMP->E5_VALOR,TMP->E2_PREFIXO,TMP->E2_NUM,TMP->E2_PARCELA,TMP->E2_TIPO,;
	TMP->E5_SEQ,TMP->E5_DATA,ALLTRIM(SA2->A2_NOME)})
	
	TMP->(DbSkip())
EndDo

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ordena o array por ordem de Cheque. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aSort(aProdutor,,,{|x, y| x[1] < y[1]})
aSort(aTitulos,,,{|x, y| x[1] < y[1]})

DbSelectArea("TMP")
DbCloseArea("TMP")

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณ CancBxSE2ณ Autor ณMicrosiga              ณ Data ณ 00.00.00 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Cancela Baixa titulo no contas a pagar via SigaAuto.       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณ ExpN01 - Valor a ser baixado no titulo.                    ณฑฑ
ฑฑณ          ณ ExpC02 - Prefixo do titulo a ser baixado.                  ณฑฑ
ฑฑณ          ณ ExpC03 - Numero do titulo a ser baixado.                   ณฑฑ
ฑฑณ          ณ ExpC04 - Parcela do titulo a ser baixado.                  ณฑฑ
ฑฑณ          ณ ExpC05 - Tipo do titulo a ser baixado.                     ณฑฑ
ฑฑณ          ณ ExpC06 - Sequencia de Baixa.                               ณฑฑ
ฑฑณ          ณ                                                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ GENERICO                                                   ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CancBxSE2(nVlrBx,cPrefixo,cNroTit,cParcela,cTipo,cSeq,dData)

Local nModAnt := nModulo
Local cModAnt := cModulo
Local aAreaTRB:= TRB->(GetArea())
Local aAreaSA2:= SA2->(GetArea())

Private lMsErroAuto:= .F.
Private lMsHelpAuto:= .T.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Posiciona no titulo antes de executar o cancelamento da baixa. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SE2")
DbSetOrder(1)
If DbSeek(xFILIAL("SE2")+cPrefixo+cNroTit+cParcela+cTipo+SA2->A2_COD+SA2->A2_LOJA)
	
	aTitulo := {{"E2_PREFIXO",cPrefixo     ,Nil},;
	{"E2_NUM"	    ,cNroTit	            ,Nil},;
	{"E2_PARCELA"   ,cParcela               ,Nil},;
	{"E2_TIPO"	    ,cTipo                  ,Nil},;
	{"E2_FORNECE"   ,SA2->A2_COD            ,Nil},;
	{"E2_LOJA"	    ,SA2->A2_LOJA           ,Nil},;
	{"AUTJUROS"		,0				        ,Nil},;
	{"AUTDESCONT"	,0		 		        ,Nil},;
	{"AUTMOTBX"		,"NOR"                  ,Nil},;
	{"AUTDTBAIXA"	,dData                  ,Nil},;
	{"AUTDTCREDITO"	,dData                  ,Nil},;
	{"AUTHIST"		,"Cancto Bx - "+SA2->A2_COD+SA2->A2_LOJA,Nil},;
	{"AUTVLRPG"		,nVlrBx	                ,Nil},;
	{"AUTVALREC"	,nVlrBx	                ,Nil}}
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Altera o modulo para Financeiro, senao o SigaAuto nao executa.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nModulo := 6
	cModulo := "FIN"
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Busca o numero da Baixa.                                      ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nBaixa := NroSeq(cPrefixo,cNroTit,cParcela,cTipo,SA2->A2_COD,SA2->A2_LOJA,cSeq)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ SigaAuto de Cancelamento de Baixa de Contas a Pagar. ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	MSExecAuto( {|x,y,z,k| Fina080(x,y,z,k)},aTitulo,5,,nBaixa)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Restaura o modulo em uso. ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nModulo := nModAnt
	cModulo := cModAnt
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Verifica se houve erro no SigaAuto, caso haja mostra o erro. ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lMsErroAuto
		lDeuErro := .T.
		xMagHelpFis("SIGAAUTO BAIXA TITULO",;
		"Existe uma nใo conformidade no SigaAuto de CANCELAMENTO de Baixa de Contas a Pagar!",;
		xFilial("SE2")+cPrefixo+cNroTit+cParcela+cTipo+SA2->A2_COD+SA2->A2_LOJA+;
		"     <-  Copie essas informa็๕es para voce pesquisar no Contas a Pagar o titulo que esta com nใo conformidade. "+;
		"Ap๓s confirmar esta tela, sera apresentada a tela de Nใo Conformidade do SigaAuto.")
		MostraErro()
	EndIf
EndIf

RestArea(aAreaSA2)
RestArea(aAreaTRB)

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณ CancCheq ณ Autor ณMicrosiga              ณ Data ณ 00.00.00 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Cancela o cheque dos titulos.                              ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณ ExpC1 = Banco do cheque.                                   ณฑฑ
ฑฑณ          ณ ExpC2 = Agencia do cheque.                                 ณฑฑ
ฑฑณ          ณ ExpC3 = Conta do cheque.                                   ณฑฑ
ฑฑณ          ณ ExpC4 = Numero do cheque.                                  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ .T. deu erro; .F. nao deu erro.                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ GENERICO                                                   ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CancCheq(cBco,cAg,cCta,cChq)

Local nReg       := 0
Local lDeleta    := .F. // Contr. Delecao do SEF
Local lDeletaSE5 := .F. // Indica se o SE5 sera deletado
Local nProxChq   := 0
Local nEfValor   := 0
Local nRegSE5    := 0
Local cChave     := xFilial("SEF")+cBco+cAg+cCta+cChq
Local dEfData    := Ctod("")
Local aTitulo    := {}
Local cNatur190  := GetMv("MV_NTCHEST")		// Natureza Cheque estornado
Local lBxConc  	 := GetNewPar("MV_BXCONC","2") == "1"
Local cFilialSe2 := xFilial("SE2")
Local cFilialSe5 := xFilial("SE5")
Local cFilialSef := xFilial("SEF")
Local lNaturChq	 := .F.
Local aAreaSA2   := SA2->(GetArea("SA2"))
Local aAreaSE2   := SE2->(GetArea("SE2"))
Local aAreaSE5   := SE5->(GetArea("SE5"))
Local aAreaSEF   := SEF->(GetArea("SEF"))

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Posiciona no cheque. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SEF")
DbSetOrder(1)//EF_FILIAL+EF_BANCO+EF_AGENCIA+EF_CONTA+EF_NUM
DbSeek(xFILIAL("SEF")+cBco+cAg+cCta+cChq)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Percorre todos os registros do cheque para encontrar o registro totalizador do cheque. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
While SEF->(!Eof()) .And. ALLTRIM(SEF->(EF_FILIAL+EF_BANCO+EF_AGENCIA+EF_CONTA+EF_NUM)) == ALLTRIM(xFILIAL("SEF")+cBco+cAg+cCta+cChq)
	If SEF->EF_IMPRESS <> "A" .AND. SEF->EF_IMPRESS <> "C" .AND. nReg == 0
		nReg := SEF->(Recno())
	EndIf
	SEF->(DbSkip())
EndDo

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Posiciona no cheque para fazer o cancelamento.                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SEF")
DbGoTo(nReg)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se data do movimento nao eh menor que data limite de ณ
//ณ movimentacao no financeiro.                                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !DtMovFin()
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se data da exclusao do cheque, maior q. a data sist.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If SEF->EF_DATA > dDataBase
	Help(" ",1,"DTCANCH")
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se o cheque ja foi cancelado.                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( SEF->EF_IMPRESS == "C" )
	Help(" ",1,"JA CANCELA")
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ So cancela os cheques gerados pelo FINA080.                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( Empty(SEF->EF_NUM) .And. !(AllTrim(SEF->EF_ORIGEM)$"FINA100PAG$FINA050") )
	Help(" ",1,"NAO GERADO")
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se vc esta posicionado no cheque ou no SEF dos titulos. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( SEF->EF_IMPRESS == "A" )
	Help(" ",1,"AGLUTINADO")
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Posiciona nos movimentos bancarios para verificar as conciliacoes. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
SE5->(dbSetOrder(11))
If SE5->(dbSeek(xfilial("SE5")+SEF->EF_BANCO+SEF->EF_AGENCIA+SEF->EF_CONTA+SEF->EF_NUM,.T.) )
	While ( SE5->(!Eof()).And. xfilial("SE5")==SE5->E5_FILIAL .And.	SE5->E5_BANCO   == SEF->EF_BANCO   .And. ;
		SE5->E5_AGENCIA == SEF->EF_AGENCIA .And. ;
		SE5->E5_CONTA   == SEF->EF_CONTA .and. ;
		SE5->E5_NUMCHEQ == SEF->EF_NUM )
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Verifica se a baixa ja foi conciliada. ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !Empty(SE5->E5_RECONC) .And. SE5->E5_TIPODOC=="CH"
			Help(" ",1,"BXCONCIL")
			Return(.T.)
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Nao permito cancelamento de baixa se a mesma foi conciliada e seณ
		//ณ o parametro MV_BXCONC estiver como 2(Padrao) - Nao permite.     ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !Empty(SE5->E5_RECONC) .And. !lBxConc .And. SE5->E5_TIPODOC=="CH" .AND. !F190EstCh()
			Help(" ",1,"BXCONCIL")
			Return(.T.)
		EndIf
		
		SE5->(dbSkip())
	EndDo
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica a origem e o tipo do cheque. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( !Empty(SEF->EF_ORIGEM) .And.;
	!(AllTrim(SEF->EF_ORIGEM)$"FINA080#FINA190#FINA050#FINA090") .And. ;
	!(AllTrim(SEF->EF_ORIGEM)$"FINA070" .And. AllTrim(SEF->EF_TIPO)$MV_CRNEG) .And. ;
	!(AllTrim(SEF->EF_ORIGEM)$"FINA100PAG" .And. Empty(SEF->EF_NUM)) ) .or. ;
	SEF->EF_TIPO $ MVPAGANT
	Help(" ",1,"ORIGEM190")
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Salva a ordem utilizada na pesquisa e ativa a ordem 1, mais ณ
//ณ apropriada  a tarefa de cancelamento do cheque.             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
dbSelectArea("SEF")
dbSetOrder(1)

If !SoftLock( "SEF" )
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica bloqueio de conta corrente. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SEF")
If CCBLOCKED(EF_BANCO,EF_AGENCIA,EF_CONTA)
	Return(.T.)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se tem filial de origem e se a tabela NAO eh compartilhada. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Empty(SEF->EF_FILORIG) .And. !(Empty(xFilial("SEF")))
	cFilialSef := SEF->EF_FILORIG
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica a origem do titulo para posicionar no SEF.                  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( !AllTrim(SEF->EF_ORIGEM) $ "FINA100PAG#FINA050" )
	dbSelectArea("SEF")
	dbSetOrder( 1 )
	SEF->( dbSeek(cFilialSef+cBco+cAg+cCta+cChq,.F.) )
EndIf

While ( !Eof() .And. cFilialSef+SEF->EF_BANCO+SEF->EF_AGENCIA+;
	SEF->EF_CONTA+SEF->EF_NUM == cChave )
	
	SEF->( dbSkip() )
	nProxChq := SEF->( Recno() )
	SEF->( dbSkip(-1) )
	
	If ( SEF->EF_IMPRESS == "A" )
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Quando o Cheque for Aglutinado deve-se limpar seus dados    ณ
		//ณ mas nao pode-se apaga-lo, pois nao foi gerado pelo FINA190  ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Reclock("SEF",.F.)
		If !Empty(SEF->EF_FILORIG)
			SEF->EF_FILIAL := SEF->EF_FILORIG
		EndIf
		If lNaturChq
			SEF->EF_NATUR := cNatur190
		EndIf
		SEF->EF_NUM     := ""
		SEF->EF_IMPRESS := ""
		SEF->EF_LIBER   := ""
		MsUnlock()
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Limpa Numero do Cheque nos registros do SE5                 ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE2")))
			cFilialSe2 := SEF->EF_FILORIG
		EndIf
		
		dbSelectArea("SE2")
		dbSetOrder(1)
		If dbSeek(cFilialSe2+SEF->EF_PREFIXO+SEF->EF_TITULO+;
			SEF->EF_PARCELA+SEF->EF_TIPO+SEF->EF_FORNECE+SEF->EF_LOJA)
			
			If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE5")))
				cFilialSe5 := SEF->EF_FILORIG
			EndIf
			
			dbSelectArea("SE5")
			SE5->( dbSetOrder(7))
			SE5->( dbSeek(cFilialSe5+SEF->(EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO)+SE2->E2_FORNECE+SE2->E2_LOJA+SEF->EF_SEQUENC))
			
			While SE5->( !Eof() ) .And. cFilialSe5+SEF->(EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO)+SE2->E2_FORNECE+SE2->E2_LOJA=;
				E5_FILIAL+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA
				
				If ( SE5->E5_CLIFOR+SE5->E5_LOJA == SE2->E2_FORNECE+SE2->E2_LOJA ) .and. ;
					Substr(SE5->E5_BANCO,1,2) != "CX" .and. ;
					!(SE5->E5_BANCO $ GetMv("MV_CARTEIR")) .and. ;
					SE5->E5_SEQ == SEF->EF_SEQUENC
					
					RecLock("SE5",.F.)
					SE5->E5_NUMCHEQ   := ""
					MsUnlock()
					
				EndIf
				
				SE5->(dbSkip())
			EndDo
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Monta array com titulos do cheque               ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Aadd(aTitulo,{ SEF->EF_PREFIXO,;
		SEF->EF_TITULO ,;
		SEF->EF_PARCELA,;
		SEF->EF_TIPO   ,;
		SEF->EF_VALOR  ,;
		SEF->EF_FORNECE,;
		SEF->EF_LOJA	,;
		SEF->(Recno())})
	ElseIf ( SEF->EF_IMPRESS $ " S" )
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Quando o Cheque estiver impresso deve-se cancela-lo  		ณ
		//ณ e quando o cheque ainda nao foi impresso pode-se exclui-lo. ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If ( SEF->EF_IMPRESS == "S" )
			
			Reclock("SEF",.F.)
			If lNaturChq
				SEF->EF_NATUR := cNatur190
			EndIf
			SEF->EF_IMPRESS := "C"
			MsUnlock()
			lDeleta := .F.
			
			If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE2")))
				cFilialSe2 := SEF->EF_FILORIG
			EndIf
			
			dbSelectArea("SE2")
			dbSetOrder(1)
			If SE2->(dbSeek(cFilialSe2+SEF->EF_PREFIXO+SEF->EF_TITULO+SEF->EF_PARCELA+;
				SEF->EF_TIPO+SEF->EF_FORNECE+SEF->EF_LOJA,.F.) )
				RecLock("SE2")
				SE2->E2_NUMBCO := ""
				MsUnlock()
			EndIf
			dbSelectArea("SEF")
		Else
			lDeleta  := .T.
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Atualizacao do Saldo Bancario                 ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		dEfData  := IIf(SEF->EF_DATA > dDataBase, SEF->EF_DATA, dDataBase)
		nEfValor += SEF->EF_VALOR
		
		If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE5")))
			cFilialSe5 := SEF->EF_FILORIG
		EndIf
		
		dbSelectArea( "SE5" )
		dbSetOrder( 11 )
		SE5->( dbSeek(cFilialSe5+SEF->EF_BANCO+SEF->EF_AGENCIA+SEF->EF_CONTA+SEF->EF_NUM,.T.) )
		While ( SE5->(!Eof()) .And.	SE5->E5_BANCO   == SEF->EF_BANCO   .And. ;
			SE5->E5_AGENCIA == SEF->EF_AGENCIA .And. ;
			SE5->E5_CONTA   == SEF->EF_CONTA .and. ;
			SE5->E5_NUMCHEQ == SEF->EF_NUM )
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ So deleta quando for cheque da baixa ou da mov bancaria   ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			If ( SE5->E5_NUMCHEQ == SEF->EF_NUM .And.;
				!(SE5->E5_TIPODOC $ "VL/BA/CM/MT/DC/JR/TL/V2/D2/V2/IS/ES") .And.;
				SE5->E5_MOEDA $ "C1/C2/C3/C4/C5/CH  " )
				lDeletaSE5 := .T.
				dEfData 	  := dDatabase
				nRegSE5    := SE5->( Recno() )
			EndIf
			SE5->(dbSkip())
		EndDo
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Limpa Numero do Cheque no SE5 ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE2")))
			cFilialSe2 := SEF->EF_FILORIG
		EndIf
		
		dbSelectArea("SE2")
		dbSetOrder(1)
		If dbSeek(cFilialSe2+SEF->EF_PREFIXO+SEF->EF_TITULO+;
			SEF->EF_PARCELA+SEF->EF_TIPO+SEF->EF_FORNECE+SEF->EF_LOJA)
			
			If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE5")))
				cFilialSe5 := SEF->EF_FILORIG
			EndIf
			
			dbSelectArea("SE5")
			SE5->( dbSetOrder(7))
			SE5->( dbSeek(cFilialSe5+SEF->(EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO+EF_SEQUENC)))
			
			While SE5->( !Eof() ) .And. cFilialSe5+SEF->(EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO+EF_SEQUENC)=;
				E5_FILIAL+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_SEQ
				
				If ( SE5->E5_CLIFOR+SE5->E5_LOJA == SE2->E2_FORNECE+SE2->E2_LOJA ) .and. ;
					Substr(SE5->E5_BANCO,1,2) != "CX" .and. ;
					!(SE5->E5_BANCO $ GetMv("MV_CARTEIR")) .and. ;
					SE5->E5_SEQ == SEF->EF_SEQUENC .and. SE5->E5_TIPODOC != "VL"
					RecLock("SE5",.F.)
					SE5->E5_NUMCHEQ := ""
					MsUnlock()
				EndIf
				
				SE5->(dbSkip())
			EndDo
		EndIf
		
		If ( AllTrim(SEF->EF_ORIGEM) $ "FINA100PAG#FINA050" ) .AND. ;
			!(SEF->EF_TIPO $ MVPAGANT)
			cChave   := ""
			nEfValor := 0 // Nao atualiza Mov. Bancaria.
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Nao atualiza Saldo Bancario quando o cheque comecar com * ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If substr( cChq,1,1 ) == "*"
			nEfValor := 0
		EndIf
		
		If ( nEfValor != 0 .And. SEF->EF_LIBER != "N" )
			AtuSalBco( cBco,cAg,cCta,dEfData,nEfvalor,"+")
		EndIf
	EndIf
	
	dbSelectArea( "SEF" )
	SEF->( dbGoto(nProxChq) )
EndDo

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Posiciona no cheque para buscar os dados a serem gravados no SE5. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
dbSelectarea( "SEF" )
SEF->( dbGoTo( nReg ) )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Posiciona o Cadastro de Bancos            ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
dbSelectArea("SA6")
dbSeek(xFilial()+cBco+cAg+cCta)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se ira cancelar o SE5                      ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( lDeletaSe5 )
	dbSelectArea("SE5")
	SE5->( dbGoto(nRegSE5) )
	cNatur190 := IIf (Empty(cNatur190), SE5->E5_NATUREZ , cNatur190)
	If SE5->E5_LA == "S " .or. !lDeleta .or.;
		(SE5->E5_DTDISPO <= dDatabase .And. SEF->EF_LIBER $ "S")
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณCancela a Geracao do cheque totalizador gerando um lancamento  ณ
		//ณde estorno no SE5    										  ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Reclock("SE5",.T.)
		SE5->E5_FILIAL    := xFilial("SE5")
		SE5->E5_BANCO     := SEF->EF_BANCO
		SE5->E5_AGENCIA   := SEF->EF_AGENCIA
		SE5->E5_CONTA     := SEF->EF_CONTA
		SE5->E5_DATA      := dDatabase
		SE5->E5_VALOR     := SEF->EF_VALOR
		SE5->E5_NATUREZ   := cNatur190
		SE5->E5_RECPAG    := "R"
		SE5->E5_LA        := "N"
		SE5->E5_TIPODOC   := "EC"
		SE5->E5_BENEF     := SEF->EF_BENEF
		SE5->E5_DTDIGIT   := dDataBase
		SE5->E5_SEQ       := SEF->EF_SEQUENC
		SE5->E5_DTDISPO   := dDataBase
		SE5->E5_NUMCHEQ	  := SEF->EF_NUM
		SE5->E5_HISTOR	  := "Cancto de Cheque do Prod/Fret"
		If SpbInUse()
			SE5->E5_MODSPB := "3"
		EndIf
		MsUnlock()
	Else
		dbSelectArea("SE5")
		SE5->( dbGoto(nRegSE5) )
		Reclock("SE5",.F.)
		dbDelete()
		MsUnlock()
	EndIf
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se ira deletar cheque                      ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ( lDeleta )
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Libera o numero do cheque no SE2.                            ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If !Empty(SEF->EF_PREFIXO+SEF->EF_TITULO+SEF->EF_PARCELA)
		
		If !Empty(SEF->EF_FILORIG) .and. !(Empty(xFilial("SE2")))
			cFilialSe2 := SEF->EF_FILORIG
		EndIf
		
		DbSelectArea("SE2")
		DbSetOrder(1)
		If ( dbSeek(cFilialSe2+SEF->EF_PREFIXO+SEF->EF_TITULO+SEF->EF_PARCELA+SEF->EF_TIPO+SEF->EF_FORNECE))
			Reclock("SE2",.F.)
			SE2->E2_NUMBCO := ""
			MsUnlock()
		EndIf
		
	EndIf
	Reclock("SEF",.F.)
	SEF->( dbDelete() )
	MsUnlock()
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Restaura a area dos arquivos. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
RestArea(aAreaSA2)
RestArea(aAreaSE2)
RestArea(aAreaSE5)
RestArea(aAreaSEF)

Return(.F.)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณ NroSeq   บ Autor ณ Jeovane               บ Data da Criacao  ณ 19/11/2008                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณ Busca numero de sequencia da baixa no array de baixas do titulo                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ cancBaixa                       						                                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ cPrefixo   = Prefixo do titulo a ser cancelado a baixa                                                       บฑฑ
ฑฑบ          ณ cNum       = Numero do titulo a ser cancelado a baixa                                                        บฑฑ
ฑฑบ          ณ cParc      = Parcela do titulo a ser cancelado a baixa                                                       บฑฑ
ฑฑบ          ณ cTipo      = Tipo do titulo a ser cancelado a baixa                                                          บฑฑ
ฑฑบ          ณ cFor       = Fornecedor do titulo a ser cancelado a baixa                                                    บฑฑ
ฑฑบ          ณ cLoja      = Loja do Fornecedor do titulo a ser cancelado a baixa                                            บฑฑ
ฑฑบ          ณ cSeq       = Sequencia da baixa                                                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Numero da baixa a ser cancelado, este numero pode ser diferente do E5_SEQ                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite.                                                                        						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ00/00/0000ณ                               				   ณ00-000000 -                       ณ TI	        บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function NroSeq(cPrefixo,cNum,cParcela,cTipo,cFor,cLoja,cSeq)

Local nRet := 0
Local nPos := 0

Private lBaixaAbat:= .F.
Private lNotBax   := .F.
Private lAglImp   := .F.
Private lBxCec    := .F.
Private nTotImpost:= 0
Private nTotAdto  := 0
Private aBaixaSE5 := {}

DbSelectArea("SE2")
DbSetOrder(1)
DbSeek(xFilial("SE2")+cPrefixo+cNum+cParcela+cTipo+cFor+cLoja)

//Funcao Padrao do Sistema que retorna um array com as baixas a serem canceladas
aBaixaSE5 := Sel080Baixa("VL /V2 /BA /RA /CP /LJ /NCC/",cPrefixo,cNum,cParcela,cTipo,@nTotAdto,@lBaixaAbat,cFor,cLoja,@lBxCec,.T.,@lNotBax,@nTotImpost,@lAglImp)
For nPos := 1 to len(aBaixaSE5)
	If Substr(aBaixaSE5[nPos],LEN(aBaixaSE5[nPos])-1,2) == cSeq
		nRet := nPos
		Exit
	EndIf
Next nPos

Return nRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ AjustaSX1บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Funcao para criacao das perguntas caso elas nao existam.   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Programa principal                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function AjustaSX1()

Local aHelpPor := {}
Local aHelpSpa := {}
Local aHelpEng := {}

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o codigo do Banco do Cheque.   ')
Aadd( aHelpSpa, 'Informe o codigo do Banco do Cheque.   ')
Aadd( aHelpEng, 'Informe o codigo do Banco do Cheque.   ')
U_xPutSx1(cPerg,"01","Banco Cheque","Banco Cheque","Banco Cheque","mv_ch1","C",3,0,0,"G",'ExistCpo("SA6")',"SA6","","","mv_par01","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o codigo da Agencia do Cheque. ')
Aadd( aHelpSpa, 'Informe o codigo da Agencia do Cheque. ')
Aadd( aHelpEng, 'Informe o codigo da Agencia do Cheque. ')
U_xPutSx1(cPerg,"02","Ag. Cheque","Ag. Cheque","Ag. Cheque","mv_ch2","C",5,0,0,"G","","","","","mv_par02","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o codigo da Conta do Cheque.   ')
Aadd( aHelpSpa, 'Informe o codigo da Conta do Cheque.   ')
Aadd( aHelpEng, 'Informe o codigo da Conta do Cheque.   ')
U_xPutSx1(cPerg,"03","Cta. Cheque","Cta. Cheque","Cta. Cheque","mv_ch3","C",10,0,0,"G","","","","","mv_par03","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo do MIX que sera utili-')
Aadd( aHelpPor, 'zado para o Fechamento.                ')
Aadd( aHelpPor, 'ATENCAO: Escolha um MIX aberto e com   ')
Aadd( aHelpPor, 'periodo correto.                       ')
Aadd( aHelpSpa, 'Informe o Codigo do MIX que sera utili-')
Aadd( aHelpSpa, 'zado para o Fechamento.                ')
Aadd( aHelpSpa, 'ATENCAO: Escolha um MIX aberto e com   ')
Aadd( aHelpSpa, 'periodo correto.                       ')
Aadd( aHelpEng, 'Informe o Codigo do MIX que sera utili-')
Aadd( aHelpEng, 'zado para o Fechamento.                ')
Aadd( aHelpEng, 'ATENCAO: Escolha um MIX aberto e com   ')
Aadd( aHelpEng, 'periodo correto.                       ')
U_xPutSx1(cPerg,"04","Codigo do MIX ?","Codigo do MIX ?","Codigo do MIX ?","mv_ch4","C",6,0,0,"G",'ExistCpo("ZLE")',"ZLE_01","","","mv_par04","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Versao do MIX. Versao 1 significa que  ')
Aadd( aHelpPor, 'se trata do 1o Fechto. Algumas filiais ')
Aadd( aHelpPor, 'terao mais de um fechamento por mes.   ')
Aadd( aHelpSpa, 'Versao do MIX. Versao 1 significa que  ')
Aadd( aHelpSpa, 'se trata do 1o Fechto. Algumas filiais ')
Aadd( aHelpSpa, 'terao mais de um fechamento por mes.   ')
Aadd( aHelpEng, 'Versao do MIX. Versao 1 significa que  ')
Aadd( aHelpEng, 'se trata do 1o Fechto. Algumas filiais ')
Aadd( aHelpEng, 'terao mais de um fechamento por mes.   ')
U_xPutSx1(cPerg,"05","Versao MIX de?","Versao MIX de?","Versao MIX de?","mv_ch5","C",1,0,0,"G",,,"","","mv_par05","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Versao do MIX. Versao 1 significa que  ')
Aadd( aHelpPor, 'se trata do 1o Fechto. Algumas filiais ')
Aadd( aHelpPor, 'terao mais de um fechamento por mes.   ')
Aadd( aHelpSpa, 'Versao do MIX. Versao 1 significa que  ')
Aadd( aHelpSpa, 'se trata do 1o Fechto. Algumas filiais ')
Aadd( aHelpSpa, 'terao mais de um fechamento por mes.   ')
Aadd( aHelpEng, 'Versao do MIX. Versao 1 significa que  ')
Aadd( aHelpEng, 'se trata do 1o Fechto. Algumas filiais ')
Aadd( aHelpEng, 'terao mais de um fechamento por mes.   ')
U_xPutSx1(cPerg,"06","Versao MIX Ate?","Versao MIX Ate?","Versao MIX Ate?","mv_ch6","C",1,0,0,"G",,,"","","mv_par06","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe os codigos dos setores a serem ')
Aadd( aHelpPor, 'considerados no Acerto. Se deixado em  ')
Aadd( aHelpPor, 'branco, serao considerados todos.      ')
Aadd( aHelpSpa, 'Informe os codigos dos setores a serem ')
Aadd( aHelpSpa, 'considerados no Acerto. Se deixado em  ')
Aadd( aHelpSpa, 'branco, serao considerados todos.      ')
Aadd( aHelpEng, 'Informe os codigos dos setores a serem ')
Aadd( aHelpEng, 'considerados no Acerto. Se deixado em  ')
Aadd( aHelpEng, 'branco, serao considerados todos.      ')
U_xPutSx1(cPerg,"07","Setor ?","Setor ?","Setor ?","mv_ch7","C",60,0,0,"G",'VAZIO().OR.SUBSTR(MV_PAR07,LEN(ALLTRIM(MV_PAR07)),1)=="/"',"U_F302","","","mv_par07","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo do Prod/Fretist p/ ser')
Aadd( aHelpPor, 'considerado no Acerto. Vc pode informar')
Aadd( aHelpPor, 'de " " a ZZZZZZ, para considerar todos ')
Aadd( aHelpPor, 'ou entao do P00001 ao P00001 para      ')
Aadd( aHelpPor, 'considerar apenas este produtor.       ')
Aadd( aHelpSpa, 'Informe o Codigo do Prod/Fretist p/ ser')
Aadd( aHelpSpa, 'considerado no Acerto. Vc pode informar')
Aadd( aHelpSpa, 'de " " a ZZZZZZ, para considerar todos ')
Aadd( aHelpSpa, 'ou entao do P00001 ao P00001 para      ')
Aadd( aHelpSpa, 'considerar apenas este produtor.       ')
Aadd( aHelpEng, 'Informe o Codigo do Prod/Fretist p/ ser')
Aadd( aHelpEng, 'considerado no Acerto. Vc pode informar')
Aadd( aHelpEng, 'de " " a ZZZZZZ, para considerar todos ')
Aadd( aHelpEng, 'ou entao do P00001 ao P00001 para      ')
Aadd( aHelpEng, 'considerar apenas este produtor.       ')
U_xPutSx1(cPerg,"08","Prod/Fret de?","Prod/Fret de?","Prod/Fret de?","mv_ch8","C",6,0,0,"G",,"SA2_L6","","","mv_par08","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo do Prod/Fret para ser ')
Aadd( aHelpPor, 'considerado no Acerto.                 ')
Aadd( aHelpSpa, 'Informe o Codigo do Prod/Fret para ser ')
Aadd( aHelpSpa, 'considerado no Acerto.                 ')
Aadd( aHelpEng, 'Informe o Codigo do Prod/Fret para ser ')
Aadd( aHelpEng, 'considerado no Acerto.                 ')
U_xPutSx1(cPerg,"09","Prod/Fret Ate?","Prod/Fret Ate?","Prod/Fret Ate?","mv_ch9","C",6,0,0,"G",,"SA2_L6","","","mv_par09","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo da Loja do Prod/Fretis')
Aadd( aHelpSpa, 'Informe o Codigo da Loja do Prod/Fretis')
Aadd( aHelpEng, 'Informe o Codigo da Loja do Prod/Fretis')
U_xPutSx1(cPerg,"10","Loja de?","Loja de?","Loja de?","mv_cha","C",2,0,0,"G",,"","","","mv_par10","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo da Loja do Prod/Fretis')
Aadd( aHelpSpa, 'Informe o Codigo da Loja do Prod/Fretis')
Aadd( aHelpEng, 'Informe o Codigo da Loja do Prod/Fretis')
U_xPutSx1(cPerg,"11","Loja Ate?","Loja Ate?","Loja Ate?","mv_chb","C",2,0,0,"G",,"","","","mv_par11","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo da Rota/Linha inicial ')
Aadd( aHelpSpa, 'Informe o Codigo da Rota/Linha inicial ')
Aadd( aHelpEng, 'Informe o Codigo da Rota/Linha inicial ')
U_xPutSx1(cPerg,"12","Rota/Linha de?","Rota/Linha de?","Rota/Linha de?","mv_chc","C",6,0,0,"G",,"ZL3_01","","","mv_par12","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

aHelpPor := {}
aHelpSpa := {}
aHelpEng := {}
Aadd( aHelpPor, 'Informe o Codigo da Rota/Linha final.  ')
Aadd( aHelpSpa, 'Informe o Codigo da Rota/Linha final.  ')
Aadd( aHelpEng, 'Informe o Codigo da Rota/Linha final.  ')
U_xPutSx1(cPerg,"13","Rota/Linha Ate?","Rota/Linha Ate?","Rota/Linha Ate?","mv_chd","C",6,0,0,"G",,"ZL3_01","","","mv_par13","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)

Return