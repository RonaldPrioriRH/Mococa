#INCLUDE "totvs.ch"
#INCLUDE "fileio.ch"
#INCLUDE "topconn.ch"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRFATR002  บAutor  ณTarcisio Silva MirandaบDataณ  05/01/2018 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Rotina de inclusใo de containner na Sf2.	    			  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Ap๓s grava็ใo da Sf2.                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function RFATR002()

// Declaracao de Variaveis
Local 	aArea 		:= GetArea()
Local	cQry        := ""
Local 	cEol        := chr(13)+chr(10)
Local   cIdDe    	:= ""
Local 	cIdAte   	:= ""
Local 	nMovEst		:= 0
Local 	nMovFin		:= 0

////////////////////////////////////////////////////////////////////////
///Outras Variaveis
////////////////////////////////////////////////////////////////////////
Private cLocFile    := ""
Private cPerg       := "RFATR002"

////////////////////////////////////////////////////////////////////////
///Cria as perguntas no SX1
////////////////////////////////////////////////////////////////////////
ValPer01()

if !Pergunte(cPerg,.t.)
	return
endif

cIdDe	     	:= mv_par01
cIdAte	    	:= mv_par02
cTpProdDe		:= mv_par03
cTpProdAte		:= mv_par04
nMovEst			:= mv_par05
nMovFin			:= mv_par06
cLocFile 	    := mv_par07


cQry+=" SELECT F2_FILIAL AS FILIAL,F2_DOC AS NFDESAIDA,A1_COD AS CODCLIENTE,A1_LOJA AS LOJA,A1_NOME AS NOME, " +cEOL
cQry+=" A1_EST AS ESTADO,A1_CGC AS CPFCNPJ,F2_XIDTRI AS IDTRIANGULACAO,F2_CARGA AS CARGA,B1_COD AS CODPRODUTO, " +cEOL
cQry+=" B1_DESC AS DESCRICAO,SUM(D2_QUANT) AS QUANT,SUM(D2_PESO) AS PESO, F2_VALBRUT VALBRUT  " +cEOL
cQry+=" FROM " +RetSqlName("SF2") 
cQry+=" LEFT JOIN "+ RetSqlName("SC5") +" ON SC5010.D_E_L_E_T_ <> '*' AND C5_XIDTRI = F2_XIDTRI " +cEOL
cQry+=" LEFT JOIN "+ RetSqlName("SD2") +" ON SD2010.D_E_L_E_T_ <> '*' AND F2_DOC = D2_DOC AND F2_FILIAL = D2_FILIAL  " +cEOL
cQry+=" LEFT JOIN "+ RetSqlName("SB1") +" ON SB1010.D_E_L_E_T_ <> '*' AND B1_COD = D2_COD  " +cEOL
cQry+=" LEFT JOIN "+ RetSqlName("SA1") +" ON SA1010.D_E_L_E_T_ <> '*' AND A1_COD = F2_CLIENTE AND A1_LOJA = F2_LOJA " +cEOL
cQry+=" LEFT JOIN "+ RetSqlName("SF4") +" ON SF4010.D_E_L_E_T_ <> '*' AND F4_CODIGO = D2_TES AND F4_FILIAL = D2_FILIAL " +cEOL
cQry+=" WHERE SF2010.D_E_L_E_T_ <> '*'  " +cEOL
cQry+=" AND F2_XIDTRI BETWEEN '"+cIdDe+"' AND '"+cIdAte+"' 											" +cEOL
cQry+=" AND A1_CGC NOT IN ('52502507006269','52502507006269','52502507000309','52502507006854', 	" +cEOL
cQry+=" 	'52502507001208','52502507006692','52502507000813','26687598000122','52502507001895',	" +cEOL
cQry+=" 	'52502507001623','52502507002271','52502507001976','52502507000147','52502507001380',	" +cEOL
cQry+=" 	'26687598000203','26687598000475','26687598000394','52502507000651')					" +cEOL
cQry+=" AND B1_TIPO BETWEEN '"+cTpProdDe+"' AND '"+cTpProdAte+"' 									" +cEOL

	if nMovFin == 1
		cQry+=" AND F4_DUPLIC = 'S' 						" +cEOL
	elseif nMovFin == 2
		cQry+=" AND F4_DUPLIC = 'N' 						" +cEOL
	endif
	
	if nMovEst == 1
		cQry+=" AND F4_ESTOQUE = 'S' 						" +cEOL
	elseif nMovEst == 2
		cQry+=" AND F4_ESTOQUE = 'N' 						" +cEOL
	endif

cQry+=" GROUP BY F2_FILIAL,F2_DOC,A1_COD,A1_LOJA,A1_EST,A1_CGC,F2_XIDTRI,F2_CARGA,B1_COD,B1_DESC,D2_QUANT,D2_PESO,A1_NOME,F2_VALBRUT  " +cEOL

MemoWrite("C:\temp\RFINR002.sql",cQry)

cQry := ChangeQuery(cQry)

if Select("TRB") > 0
	TRB->(DbCloseArea())
endif

TcQuery cQry New Alias "TRB"

dbSelectArea("TRB")
TRB->(dbGoTop())


Processa( {|| SPCPR01Excel() }, "Aguarde...", "Gerando planilha eletronica com as informa็๕es...",.F.)


TRB->(DbCloseArea())
RestArea(aArea)
Return

/*??????????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????????????
???????????????????????????????????????????????????????????????????????????????????
???Programa   ? ValPerg()  ? Autor ? Diego Bueno               ? Data ?20/03/2014???
????????????????????????????????????????????????????????????????????????????????J??
???Descricao  ? Cria perguntas no SX1	                                         ???
????????????????????????????????????????????????????????????????????????????????J??
???Observacao ?                                                                  ???
???           ?                                                                  ???
???????????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????????????
??????????????????????????????????????????????????????????????????????????????????*/

Static Function ValPer01()
_sAlias	:=	Alias()
//Retirado Sangelles 22/03/2022 ->dbSelectArea("SX1")
//Retirado Sangelles 22/03/2022 ->dbSetOrder(1)
cPerg 	:=	PADR(cPerg,10)

U_xPutSx1(cPerg,"01","Id De ?","."     ,"."       ,"mv_CH1","C",10,0,0,"G","","","","","MV_PAR01","","","","","","","","","","","","","","","","")
U_xPutSx1(cPerg,"02","Id At้ ?","."     ,"."       ,"mv_CH2","C",10,0,0,"G","","","","","MV_PAR02","","","","","","","","","","","","","","","","")
U_xPutSx1(cPerg,"03","Tipo De Produto De ?","."     ,"."       ,"mv_CH3","C",02,0,0,"G","","02","","","MV_PAR03","","","","","","","","","","","","","","","","")
U_xPutSx1(cPerg,"04","Tipo De Produto At้ ?","."     ,"."       ,"mv_CH4","C",02,0,0,"G","","02","","","MV_PAR04","","","","","","","","","","","","","","","","")
U_xPutSx1(cPerg,"05","Tes Movimenta Estoque ?","."     ,".","mv_CH5","N",01,0,0,"C","","","","","MV_PAR05","Sim","Sim","Sim","Nใo","Nใo","Nใo","Ambas","Ambas","Ambas","","","",,,)
U_xPutSx1(cPerg,"06","Tes Movimenta Financeiro ?","."     ,".","mv_CH6","N",01,0,0,"C","","","","","MV_PAR06","Sim","Sim","Sim","Nใo","Nใo","Nใo","Ambas","Ambas","Ambas","","","",,,)
U_xPutSx1(cPerg,"07","Salvar Em ?","."     ,"."       ,"mv_CH7","C",50,0,0,"G","","","","","MV_PAR07","","","","","","","","","","","","","","","","")

//Retirado Sangelles 22/03/2022 ->dbSelectArea(_sAlias)

Return()

/*
?????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
???Programa  SPCPR01Excel   ?Autor ?Diego Bueno      ? Data ?  20/03/14   ???
????????????????????????????????????????????????????????????????????????????
???Desc.     ? Gera a impress?o em planilha eletronica.                   ???
???          ?                                                            ???
????????????????????????????????????????????????????????????????????????????
???Uso       ? Concebra                                                   ???
????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function SPCPR01Excel()
Local oFWMSExcel := FWMSExcel():New()
Local oMsExcel
Local aCells
Local cType
Local cColumn
Local cFile
Local cFileTMP
//Local cPicture
Local lTotal
Local nRow
Local nRows
Local nField
Local nFields
Local nAlign
Local nFormat
Local uCell
Local _aHeadXls :={}
Local _aColsXls :={}

Local cWorkSheet := "Relat๓rio de Rastreabilidade "
Local cTable     := "Relat๓rio de Rastreabilidade "+DtoC(Date())+" as "+Time()
//Local lTotalize  := .F.
//Local lPicture   := .F.

dbSelectArea("TRB")
TRB->(dbGoTop())

ProcRegua(TRB->(Reccount()) )
																																	
_aHeadXls := {"Filial", "Nota Fiscal",'Cod.Cliente','Lj.Cliente',"Nome do Cliente","Estado", "Cnpj", "Id de Triangula็ใo", "Carga", "Cod.Produto","Descri็ใo","Quantidade","Valor Total","Peso"}

While !TRB->(Eof())
	
	IncProc("Processando Relat๓rio: " + TRB->NFDESAIDA + "-" + TRB->IDTRIANGULACAO)
	
	aAdd(_aColsXls,{TRB->FILIAL,TRB->NFDESAIDA,TRB->CODCLIENTE,TRB->LOJA,TRB->NOME,TRB->ESTADO,TRB->CPFCNPJ,TRB->IDTRIANGULACAO, TRB->CARGA, TRB->CODPRODUTO,TRB->DESCRICAO,TRB->QUANT,TRB->VALBRUT,TRB->PESO})
	
TRB->(DbSkip())
	
End

If Len(_aColsXls) > 0
	
	BEGIN SEQUENCE
	
	oFWMSExcel:AddworkSheet(cWorkSheet)
	oFWMSExcel:AddTable(cWorkSheet,cTable)
	
	nFields := Len( _aHeadXls )
	For nField := 1 To nFields
		cType   := "C"
		cType := ValType(_aColsXls[1][nField])
		nAlign  := IF(cType=="C",1,IF(cType=="N",3,2))
		nFormat := IF(cType=="D",4,IF(cType=="N",2,1))
		cColumn := _aHeadXls[nField]
		lTotal  := .F. // ( lTotalize .and. cType == "N" )
		oFWMSExcel:AddColumn(@cWorkSheet,@cTable,@cColumn,@nAlign,@nFormat,@lTotal)
	Next nField
	
	oFWMSExcel:CBGCOLOR2LINE := '#FFFFFF'
	oFWMSExcel:CBGCOLORLINE  := '#FFFFFF'
	
	aCells := Array(nFields)
	nRows  := Len( _aColsXls )
	For nRow := 1 To nRows
		IncProc("Gerando planilha.. [Linha: "+TRANSFORM(nRow,"@E 999999")+" de "+TRANSFORM(Len(_aColsXls),"@E 999999")+" ]    ")
		For nField := 1 To nFields
			uCell := _aColsXls[nRow][nField]
			If Valtype(uCell) == "D" .AND. EMPTY(uCell)
				aCells[nField] := space(8)
			Else
				aCells[nField] := uCell
			Endif
		Next nField
		oFWMSExcel:AddRow(@cWorkSheet,@cTable,aClone(aCells))
	Next nRow
	oFWMSExcel:Activate()
	
	cFile := ( FwTemporaryTable( NIL, .F. ) + ".xls" )
	
	While File( cFile )
		cFile := ( FwTemporaryTable( NIL, .F. ) + ".xls" )
	End While
	
	oFWMSExcel:GetXMLFile( cFile )
	oFWMSExcel:DeActivate()
	
	IF .NOT.( File( cFile ) )
		MsgStop("Falha ao tentar criar arquivo "+cFile)
		cFile := ""
		BREAK
	EndIF
	
	cLocFile := AllTrim(cLocFile)
	if SubStr(cLocFile,Len(cLocFile),1) <> '\'
		cLocFile += '\'
	endif
	
	cFileTMP := ( cLocFile + cFile )
	IF .NOT.( __CopyFile( cFile , cFileTMP ) )
		MsgStop("Falha ao tentar salvar arquivo em "+cLocFile)
		fErase( cFile )
		cFile := ""
		BREAK
	EndIF
	
	fErase( cFile )
	
	cFile := cFileTMP
	
	IF .NOT.( File( cFile ) )
		MsgStop("Falha ao tentar salvar arquivo em "+cLocFile)
		cFile := ""
		BREAK
	EndIF
	
	IF .NOT.( ApOleClient("MsExcel") )
		MsgStop("Falha ao tentar abrir arquivo "+cFile+". Excel n?o instalado!")
		BREAK
	EndIF
	
	oMsExcel:= MsExcel():New()
	oMsExcel:WorkBooks:Open( cFile )
	oMsExcel:SetVisible( .T. )
	oMsExcel:= oMsExcel:Destroy()
	END SEQUENCE
	
	oFWMSExcel := FreeObj( oFWMSExcel )
	
	
else
	MsgInfo("Nใo hแ dados para serem impressos!","VERIFIQUE OS PARAMETROS")
endif

Return()
