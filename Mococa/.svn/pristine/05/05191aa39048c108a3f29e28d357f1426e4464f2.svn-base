#INCLUDE "PROTHEUS.ch"
#INCLUDE "RWMAKE.ch"

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณ MGLT020  บ Autor ณ TOTVS TM              บ Data da Criacao  ณ 00/00/0000                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณ Baixa titulo no SE1 e cria titulo do tipo NDF no contas a pagar.                                             บฑฑ
ฑฑบ          ณ                        									                               				        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Usado nos pontos de entrada SF246I ou M460FIM, e LJFIMGRV.                                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ nRecSE1: Numero do Recno do SE1                                                      						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ _lRet - .T. - Nao encontrou nenhum problema na execucao da transferencia do convenio.   						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite.                                                                        						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ          ณ                                                  ณ                                  ณ   	        บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/

User Function MGLT020(nRecSE1)

Local aArea   		:= getArea()

Local _nModAnt 		:= nModulo
Local _cModAnt 		:= cModulo
Local _cFilBkp 		:= cFilAnt  


Local _aArea    	:= {}
Local _aAlias   	:= {}     

Local _lRet 		:= .T.
            
/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณArmazena os dados do titulo gerado na SE1 para posterior uso na baixa e na geracao da SE2.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
Private _cPrefixo	:= SE1->E1_PREFIXO 
Private _cNumTit 	:= SE1->E1_NUM  
Private _cParcela	:= SE1->E1_PARCELA  
Private _cTipo   	:= SE1->E1_TIPO
Private _cCliente	:= SE1->E1_CLIENTE
Private _cLoja   	:= SE1->E1_LOJA     
Private _dEmissao   := SE1->E1_EMISSAO
Private _dVencto    := SE1->E1_VENCTO
Private _dVencRe    := SE1->E1_VENCREA
Private _nValor		:= SE1->E1_VALOR   
//Private _cCondPgto  := SE1->E1_C_COLIM
Private _nFilOrig	:= SE1->E1_FILORIG
Private _cHistorico := "CONV.INT."+SM0->M0_FILIAL
Private _cEveFil    := ALLTRIM(SuperGetMv("LT_CONVINT",.T.,""))


/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณArmaze a filial, prefixo,natureza, setor e linha que serao utilizados para gerar a SE2. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
Private _cFilAssoc  := ""
Private _cPrefSE2   := ""
Private _cNatureza  := ""        
Private _cLinha    := ""
Private _cSetor	:= ""
                       
/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณArmazena o numero da sequencia da baixa gerada que originara a SE2 corrente.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
Private _nNunSeq    := 0   

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ  Salva a area. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
CtrlArea(1,@_aArea,@_aAlias,{"SE1","SE2"})

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Altera o modulo para Financeiro, senao o SigaAuto nao executa.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
nModulo := 6
cModulo := "FIN"

/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAdicionado por Fabiano Dias da Silva para que fosse possivel            ณ
//ณpegar a Filial que vai ser gerado o contas a pagar, ou seja, a filial   ณ
//ณde fechamento do leite e o prefixo e a natureza que sera gerado         ณ
//ณo contas a pagar, este prefixo sera de acordo com a filial do associado.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/    
If !dadosSE2(_cCliente,_cLoja)
     
	Return .F.

EndIf
                       
//Inicia Transacao
//BEGIN TRANSACTION	
	/*
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณEfetua baixa do contas a receber para depois gerar o contas a pagar.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	*/
	//If BaixaSE1(nRecSE1)           			              	    
	
	_lRet:= BaixaSE1(nRecSE1)
	
	If _lRet
	
		/*
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณPosiciona na filial em que deve ser gerado o contas a pagar do ณ
		//ณAssociado ou transportador.                                    ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		*/
	    //cFilAnt:= _cFilAssoc  	    	 
	    	    		
		/*
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณInclui o titulo no contas a pagar.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		*/
				
		_lRet:= IncluiSE2()
		
		/* 
		If !IncluiSE2()
		
			DisarmTransaction()
		EndIf
		*/
	Else
		
		MsgAlert("Nao foi possivel baixar o ctas receber do Cooperado"+chr(13)+;
		" e gerar o ctas a Pagar(NDF) para acerto do leite! "+chr(13)+;
		"Favor comunicar ao departamento de informแtica do problema encontrado!","Atencao")
		
		//DisarmTransaction()
		
	EndIf	
	
//Finaliza Transacao
//END TRANSACTION

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Restaura o modulo em uso. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
nModulo := _nModAnt
cModulo := _cModAnt
	
cFilAnt := _cFilBkp

RestArea(aArea) 

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Restaura a area. ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
CtrlArea(2,_aArea,_aAlias)

Return _lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณIncluiSE2 บ Autor ณMicrosiga           บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Inclusao de Contas a Pagar, via SIGAAUTO.                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ                                                            บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function IncluiSE2()

Local _aAutoSE2   := {}
Local lMsErroAuto := .F.
Local lRet        := .T.  

Local _cParcSE2   := ""     
//Local _cNumBxSE1  := StrZero(_nNunSeq,TamSX3("E2_L_NUMBX")[1])  

Local nVlTxPer 	  := 0 //GetMv("LT_TXPER ")//Se eh debito do produtor, grava valor da Taxa de Permanencia para cobrar juros.
Local cEntrg   	  := "" //Filial de entrega da linha do produtor
        
dbSelectArea("SE2")
dbSetOrder(1)   

_cParcela:= StrZero(Val(_cParcela),TamSX3("E2_PARCELA")[1])






/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRealiza a busca do proximo codigo de parcela a ser consideradoณ
//ณno proximo titulo a ser gerado, uma vez que um mesmo titulo naณ
//ณSE1 pode sofrer mais de uma baixa.                            ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
_cParcSE2:= proxParc(_cCliente,_cLoja,_cNumTit,_cPrefSE2)

_aAutoSE2:={;
{"E2_PREFIXO",_cPrefSE2		   			,Nil},;
{"E2_NUM"    ,_cNumTit      			,Nil},;
{"E2_TIPO"   ,"NDF"          			,Nil},;
{"E2_PARCELA",_cParcSE2      			,Nil},;
{"E2_NATUREZ",_cNatureza	   			,Nil},;
{"E2_FORNECE",_cCliente      			,Nil},;
{"E2_LOJA"   ,_cLoja	      			,Nil},;
{"E2_EMISSAO",_dEmissao     			,Nil},;
{"E2_VENCTO" ,_dVencto       			,Nil},;
{"E2_VENCREA",_dVencto       		  	,Nil},; 
{"E2_HIST"   ,_cHistorico				,Nil},;
{"E2_VALOR"  ,_nValor            		,Nil},;
{"E2_PORCJUR",nVlTxPer          		,Nil},;
{"E2_DATALIB",date()            		,Nil},;
{"E2_USUALIB",ALLTRIM(cUserName)		,Nil},; 
{"E2_ORIGEM" ,"MGLT020"		   			,Nil},;   
{"E2_FILORIG" ,_nFilOrig	   			,Nil},;   
{"E2_L_LINRO",_cLinha		   			,Nil},;
{"E2_L_SETOR",_cSetor		   			,Nil},;
{"E2_L_EVENT",_cEveFil		   			,Nil},;
{"E2_L_PCSE1",_cParcela  	   			,Nil}}  //Numero da parcela do titulo da SE1 que originou este titulo na SE2



lMsErroAuto := .F.
lMsHelpAuto := .T.

MSExecAuto({|x,y| Fina050(x,y)},_aAutoSE2,3)

If lMsErroAuto
	lRet := .F.
	Mostraerro()
	DisarmTransaction()
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ BaixaSE1 บ Autor ณMicrosiga           บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Baixa de contas a Receber, via SIGAAUTO.                   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ                                                            บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function BaixaSE1(_nRecSE1)

Local  _aBaixa     	:={}

Local _cBanco     	:= "   "
Local _cAgencia   	:= "     "
Local _cConta     	:= "           "

Local _lRet       	:= .T.
Local _cMotBaixa   	:= ALLTRIM(GETMV("LT_MOTBX"))  //Motivo de baixa utilizado somente nas rotinas de transferencia entre carteiras do Leite

Local _cSequenc   	:= ""           

Private lMsErroAuto := .F.
Private lMsHelpAuto := .T.  

dbSelectArea("SE1")
SE1->(dbGoto(_nRecSE1))

_aBaixa := {;
{"E1_FILIAL"   ,xFilial("SE1")				,Nil},;
{"E1_PREFIXO"  ,_cPrefixo 					,Nil},;
{"E1_NUM"	   ,_cNumTit    				,Nil},;
{"E1_PARCELA"  ,_cParcela					,Nil},;
{"E1_TIPO"	   ,_cTipo     					,Nil},;
{"AUTBANCO"    ,_cBanco         			,Nil},;
{"AUTAGENCIA"  ,_cAgencia   				,Nil},;
{"AUTCONTA"    ,_cConta         			,Nil},;
{"AUTMOTBX"	   ,_cMotBaixa       			,Nil},;//ACERTO LOJA - NAO GERA SE5
{"AUTDTBAIXA"  ,_dEmissao 					,Nil},;
{"AUTDTCREDITO",_dEmissao 					,Nil},;
{"AUTDESCONT"  ,0     	      				,Nil},;
{"AUTMULTA"    ,0          					,Nil},;
{"AUTJUROS"    ,0         					,Nil},;
{"AUTHIST"	   ,'BX. P/ ACERTO DO LEITE'	,Nil},;
{"AUTVALREC"   ,_nValor			   			,Nil}}    

MSExecAuto({|x,y| Fina070(x,y)},_aBaixa,3)

If lMsErroAuto
	_lRet := .F.
	Mostraerro()
EndIf                

/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณBusca o numero da ultima sequencia de baixa gerada.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
_nNunSeq:= maxSeq(_cPrefixo,_cNumTit,_cParcela,_cTipo,_cCliente,_cLoja)   

If _nNunSeq <= 0
      
      xMagHelpFis("INFORMAวรO",;
      			  "Nใo foi possํvel detectar a sequencia da baixa da SE1 que sera utilizada para gerar a parcela do tํtulo abaixo: " + _ENTER +;
      			  'Titulo/Parcela: ' + _cNumTit  + '/' + _cParcela + _ENTER +;
      			  'Cliente/Loja: '   + _cCliente + '/' + _cLoja + _ENTER +;
      			  'Prefixo/Tipo: '   + _cPrefixo + '/' + _cTipo + _ENTER +;
      			  "Desta forma o tํtulo informado anteriormente nใo serแ transferido para o contas a pagar.",;
      			  "Favor copiar esta mensagem e comunicar ao departamento de informแtica do problema encontrado.")          
		
      Return .F.

EndIf     

_cSequenc:= StrZero(_nNunSeq,TamSX3("E5_SEQ")[1]) 

dbSelectArea("SE5")
//E5_FILIAL+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ                                                                                      
SE5->(dbSetOrder(7))
If SE5->(dbSeek(xFilial("SE5") + _cPrefixo + _cNumTit + _cParcela + _cTipo + _cCliente + _cLoja + _cSequenc ))

     While !(SE5->(Eof())) .And. ;
     		SE5->E5_FILIAL == xFilial("SE5") .And. ;
     		SE5->E5_PREFIXO == _cPrefixo .And. ;
     		SE5->E5_NUMERO ==  _cNumTit .And. ;
     		SE5->E5_PARCELA == _cParcela .And. ;
     		SE5->E5_TIPO == _cTipo .And. ;  
     		SE5->E5_CLIFOR == _cCliente .And. ;
     		SE5->E5_LOJA == _cLoja .And. ;
     		SE5->E5_SEQ == _cSequenc 
     		
     		RecLock("SE5",.F.)
     		
     			SE5->E5_L_SEEK:= _cFilAssoc + _cPrefSE2 + _cNatureza
     		
			SE5->(MsUnLock())        
	
	SE5->(dbSkip())     		
    EndDo     
    
    Else
    		
    		xMagHelpFis("INFORMAวรO",;
						"Nใo foi possํvel efetuar a atualiza็ใo dos dados da baixa da tabela SE5 do seguinte tํtulo abaixo: " + _ENTER +;
						'Titulo/Parcela: ' + _cNumTit  + '/' + _cParcela + _ENTER +;
      			  		'Cliente/Loja: '   + _cCliente + '/' + _cLoja + _ENTER +;
      			  		'Prefixo/Tipo: '   + _cPrefixo + '/' + _cTipo + _ENTER +;
      					"Desta forma o tํtulo informado anteriormente nใo serแ transferido para o contas a pagar.",;
      					"Favor copiar esta mensagem e comunicar ao departamento de informแtica do problema encontrado.")   
    		Return .F.

EndIf

Return(_lRet)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณ dadosSE2 บ Autor ณ FABIANO DIAS DA SILVA บ Data da Criacao  ณ 19/12/2011                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณFuncao para buscar os dados de:FILIAL do associado e PREFIXO e NATUREZA do evento relacionado a filial que se บฑฑ
ฑฑบ          ณ esta faturando para gerar o contas a pagar.				                               				        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MGLT020																				                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ _cCodAssoc - Codigo do Associado, _cLojAssoc - Codigo da loja do associado.             						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ .T. - Dados encontrados  	  							                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite.                                                                        						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ          ณ                                                  ณ                                  ณ   	        บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/

Static Function dadosSE2(_cCodAssoc,_cLojAssoc)

Local _aArea    := GetArea()

Local _lRet     := .T.  

Local _cAliasSA2:= GetNextAlias()   
Local _cAliasZL8:= "" 

Local _cBkfilial:= cFilAnt
                        
/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณQuery para selecionar a linha a que se pertence o Associado,             ณ
//ณos dois primeiros digitos indicam a filial a que se pertence o associado.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
querys(1,_cAliasSA2,_cCodAssoc,_cLojAssoc)

dbSelectARea(_cAliasSA2)
(_cAliasSA2)->(dbGoTop()) 

If !(_cAliasSA2)->(Eof())                                    	
	/*
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณInsere o setor e a linha a que se pertence o associados para incluir estesณ
	//ณdados no titulo que sera gerado no contas a pagar, para que o mesmo possa ณ
	//ณser abatido pelo rotina de fechamento.                                    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	*/
	_cLinha:= (_cAliasSA2)->A2_L_LI_RO
	_cSetor:= (_cAliasSA2)->ZL2_COD
                                                           	
	/*
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณOs dois primeiros digitos da linha indicaram a filial a que se pertenceณ
	//ณo associado, o cadastro de linha eh exclusivo e sempre os dois         ณ
	//ณprimeiros digitos indicaram a filal a que se pertence o associado.     ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	*/
//	_cFilAssoc:= "0101" + SubStr((_cAliasSA2)->A2_L_LI_RO,1,2) 
	                  
	/*
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณMuda-se para a filial do Associado para checar o cadastro de eventos			 ณ
	//ณque hoje eh compartilhado mas se algum dia passar a ser exclusivo   			 ณ
	//ณja estara posicionado na filial correta do associado para comparacao na query.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	*/
//	cFilAnt   := _cFilAssoc    
	
	_cAliasZL8:= GetNextAlias()	
	querys(2,_cAliasZL8,"","",_cBkfilial)  
	
	dbSelectArea(_cAliasZL8)
	(_cAliasZL8)->(dbGoTop())
	
	If !(_cAliasZL8)->(Eof())
	    
	 	_cPrefSE2 := (_cAliasZL8)->ZL8_PREFIX
	 	
	 	If SubStr(_cCodAssoc,1,1) == 'C' //#TRATAR LETRA		 	                                 	 		
			/*
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณNatureza no cadastro de eventos referente ao ASSOCIADO.ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			*/
	 		_cNatureza:=(_cAliasZL8)->ZL8_NATPRD   
	 		
	 		Else       
	 			/*
				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณNatureza no cadastro de eventos referente ao TRANSPORTADOR.ณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				*/	 		
	 			_cNatureza:=(_cAliasZL8)->ZL8_NATFRT 
	 		
	 	EndIf	
		
		Else
		
			xMagHelpFis("INFORMAวรO",;
						"Nใo foi encontrado um evento de conv๊nio de leite referente a filial que esta se realizando o faturamento, favor verificar se o evento ou prefixo nใo se encontram bloqueados em seu cadastro.",;
						"Nใo foi gerado o contas a pagar para ser descontado na folha de pagamento do leite, favor comunicar ao departamento de informแtica.")             
		
	    	_lRet := .F.
	
	EndIf    
	
	dbSelectArea(_cAliasZL8)
	(_cAliasZL8)->(dbCloseArea())

	Else                
	
		xMagHelpFis("INFORMAวรO",;
					"Favor checar se os cadastros de linha e de setor ligados a linha que esta amarrada ao cadastro do associado: " + _cCodAssoc + '\'+ _cLojAssoc + ", para constatar se estใo corretos.",;
					"Nใo foi gerado o contas a pagar para ser descontado na folha de pagamento do leite, favor comunicar ao departamento de informแtica.")    
	
		_lRet := .F.

EndIf

dbSelectARea(_cAliasSA2)
(_cAliasSA2)->(dbCloseArea())

cFilAnt:= _cBkfilial    

restArea(_aArea)

Return _lRet
                     
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณ querys   บ Autor ณ TOTVS                 บ Data da Criacao  ณ 19/12/2011                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณ Funcao para armazenar as querys utilizadas nesta rotina.														บฑฑ
ฑฑบ          ณ 															                               				        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MGLT020																				                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ _nOpcao - Numero da query a ser executada																	บฑฑ
ฑฑบ			 ณ _cAlias - Alias da query																						บฑฑ
ฑฑบ			 ณ _cCodAssoc - Codigo do Associado																				บฑฑ
ฑฑบ			 ณ _cLojAssoc - Loja do Associado																				บฑฑ
ฑฑบ			 ณ _cFilFatur - Filial a que se esta realizando o faturamento da nota			           						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ 							 	  							                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite.                                                                        						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ          ณ                                                  ณ                                  ณ   	        บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/

Static Function querys(_nOpcao,_cAlias,_cCodAssoc,_cLojAssoc,_cFilFatur,_cNumTit,_cPrefixo)

	Do Case
	       
		Case _nOpcao == 1		                         			
			/*
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณQuery para selecionar a linha a que se pertence o Associado,             ณ
			//ณos dois primeiros digitos indicam a filial a que se pertence o associado.ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			*/
			BeginSql alias _cAlias
				SELECT
				      SA2.A2_L_LI_RO,ZL2.ZL2_COD
				FROM
				      %Table:SA2% SA2
				      JOIN %Table:ZL3% ZL3 ON ZL3.ZL3_COD = SA2.A2_L_LI_RO
				      JOIN %Table:ZL2% ZL2 ON ZL2.ZL2_COD = ZL3.ZL3_SETOR
				WHERE
				      SA2.D_E_L_E_T_ = ' '
				      AND ZL3.D_E_L_E_T_ = ' '
				      AND ZL2.D_E_L_E_T_ = ' '
				      AND SA2.A2_MSBLQL = '2'
				      AND ZL3.ZL3_MSBLQL = '2'
				      AND ZL2.ZL2_MSBLQL = '2'
				      AND SA2.A2_COD = %Exp:_cCodAssoc%
				      AND SA2.A2_LOJA = %Exp:_cLojAssoc%  							 
	        EndSql 
	     
	    /*
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณQuery para selecionar o prefixo e a natureza do associado ou do fretistaณ
		//ณisto para geracao do contas a pagar.                                    ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		*/
	     Case _nOpcao == 2
	              	       	        
	        BeginSql alias _cAlias
		        SELECT
				      ZL8.ZL8_PREFIX,ZL8.ZL8_NATPRD,ZL8.ZL8_NATFRT
				FROM
				      %Table:ZL8% ZL8      
				      JOIN %Table:ZL6% ZL6 ON ZL6.ZL6_COD = ZL8.ZL8_PREFIX
				WHERE
				      ZL8.D_E_L_E_T_ = ' '                 
				      AND ZL6.D_E_L_E_T_ = ' '  
				      AND ZL6.ZL6_MSBLQL = '2'
				      AND ZL8.ZL8_MSBLQL = '2'
				      AND ZL8.ZL8_FILIAL = %Exp:xFilial("ZL8")%
				      AND ZL6.ZL6_FILIAL = %Exp:xFilial("ZL6")%
				      AND ZL8.ZL8_COD = %Exp:_cEveFil%	        	        
//				      AND ZL8.ZL8_FILCON = %Exp:_cFilFatur%	        	        
	       EndSql   
	       
	   /*
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณQuery para selecionar o maior codigo de parcela gerada para o ณ
		//ณtitulo a ser gerado na SE2.                                   ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		*/
	     Case _nOpcao == 3
	              	       	        
	        BeginSql alias _cAlias                 	                     			       
		        SELECT
				      NVL(MAX(E2_PARCELA),'0') MAXPARC
				FROM
				      %Table:SE2%
				WHERE
				      D_E_L_E_T_ = ' '
				      AND E2_FILIAL = %Exp:xFilial("SE2")% 
				      AND E2_TIPO = 'NDF'
				      AND E2_NUM = %Exp:_cNumTit%
				      AND E2_PREFIXO = %Exp:_cPrefixo%				      
				      AND E2_FORNECE = %Exp:_cCodAssoc%
				      AND E2_LOJA = %Exp:_cLojAssoc%	        	        
	        EndSql   
	        
    EndCase

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ CtrlArea บ Autor ณ Microsiga          บ Data ณ  00/00/00   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Static Function auxiliar no GetArea e ResArea retornando   บฑฑ
ฑฑบ          ณ o ponteiro nos Aliases descritos na chamada da Funcao.     บฑฑ
ฑฑบ          ณ Exemplo:                                                   บฑฑ
ฑฑบ          ณ Local _aArea  := {} // Array que contera o GetArea         บฑฑ
ฑฑบ          ณ Local _aAlias := {} // Array que contera o                 บฑฑ
ฑฑบ          ณ                     // Alias(), IndexOrd(), Recno()        บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑบ          ณ // Chama a Funcao como GetArea                             บฑฑ
ฑฑบ          ณ P_CtrlArea(1,@_aArea,@_aAlias,{"SL1","SL2","SL4"})         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑบ          ณ // Chama a Funcao como RestArea                            บฑฑ
ฑฑบ          ณ P_CtrlArea(2,_aArea,_aAlias)                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ nTipo   = 1=GetArea / 2=RestArea                           บฑฑ
ฑฑบ          ณ _aArea  = Array passado por referencia que contera GetArea บฑฑ
ฑฑบ          ณ _aAlias = Array passado por referencia que contera         บฑฑ
ฑฑบ          ณ           {Alias(), IndexOrd(), Recno()}                   บฑฑ
ฑฑบ          ณ _aArqs  = Array com Aliases que se deseja Salvar o GetArea บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GENERICO                                                   บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CtrlArea(_nTipo,_aArea,_aAlias,_aArqs)

Local _nN := 0

// Tipo 1 = GetArea()
If _nTipo == 1
	_aArea := GetArea()
	For _nN := 1 To Len(_aArqs)
		DbSelectArea(_aArqs[_nN])
		AAdd(_aAlias,{ _aArqs[_nN], IndexOrd(), Recno() })
	Next
	// Tipo 2 = RestArea()
Else
	For _nN := 1 To Len(_aAlias)
		DbSelectArea(_aAlias[_nN,1])
		DbSetOrder(_aAlias[_nN,2])
		DbGoto(_aAlias[_nN,3])
	Next
	RestArea(_aArea)
Endif

Return  

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณmaxSeq    บ Autor ณ Fabiano Dias          บ Data da Criacao  ณ 12/01/2012                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณFuncao usada para identificar a maior sequencia de baixa realiza isto para pegar a ultima sequencia de baixa  บฑฑ
ฑฑบ          ณrealizada que sera usada para gerar o numero da parcela do titulo no contas a pagar e depois para realizar 	บฑฑ
ฑฑบ			 ณo cancelamento especifico da baixa na SE1 poder idenficar o titulo na SE2 como somente referente a baixa que	บฑฑ
ฑฑบ			 ณesta sendo realizada no caso de um titulo que tenha mais de uma transferencia.						        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MGLT031                         						                                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ_cPrefixo - Codigo do Prefixo na SE1																			บฑฑ
ฑฑบ			 ณ_cNumTit	- Numero do titulo na SE1																			บฑฑ
ฑฑบ			 ณ_cParcela - Numero do titulo na SE1																			บฑฑ
ฑฑบ			 ณ_cTipo    - Tipo do titulo na SE1																				บฑฑ
ฑฑบ			 ณ_cCliente - Codigo do cliente na SE1																			บฑฑ
ฑฑบ			 ณ_cLoja    - Loja do cliente na SE1																		 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nenhum                                                                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite                                                                         						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ00/00/0000ณ                               				   ณ00-000000 -                       ณ TI	        บฑฑ
ฑฑบ--------- ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/

Static Function maxSeq(_cPrefixo,_cNumTit,_cParcela,_cTipo,_cCliente,_cLoja)  
               
Local _nMaxSeq   := 0  

Local _K	     := 1  

Private aBaixaSE5:= {}		

/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณSeleciona todas as baixas realizadas no titulo informado.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
Sel070Baixa( "VL /V2 /BA /RA /CP /LJ /"+MV_CRNEG,_cPrefixo,_cNumTit,_cParcela,_cTipo,,,_cCliente,_cLoja,,,,) 
/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPercorre todas as baixas realizadas do tituloณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
For _K:=1 To Len(aBaixaSE5)   
		/*
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณArmazena a sequencia da baixa.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		*/
     	_nSequenc := Val(aBaixaSE5[_K,09])
     	
     	If _nMaxSeq < _nSequenc
     	 
     		_nMaxSeq:= _nSequenc
     	
     	EndIf
 
Next _K 
                      
Return _nMaxSeq           

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออออหออออออออออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออปฑฑ
ฑฑบPrograma  ณproxParc  บ Autor ณ Fabiano Dias          บ Data da Criacao  ณ 13/01/2012                						บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออออสออออออออออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDescricao ณFuncao usada para buscar o proximo codigo de parcela disponivel, uma vez que um mesmo numero de titulo +      บฑฑ
ฑฑบ          ณparcela + prefixo + tipo + cliente + loja na SE1 podera sofrer mais de uma baixa, com isso para que nao haja 	บฑฑ
ฑฑบ			 ณproblema na geracao da SE2 a loja dos titulo da SE2 sera considerada de forma incremental.					บฑฑ
ฑฑบ			 ณ																										        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MGLT031                         						                                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ_cPrefixo  - Codigo do Prefixo na SE1																			บฑฑ
ฑฑบ			 ณ_cNumTit	 - Numero do titulo na SE1																			บฑฑ
ฑฑบ			 ณ_cCodAssoc - Codigo do cliente na SE1																			บฑฑ
ฑฑบ			 ณ_cLojAssoc - Loja do cliente na SE1																		 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nenhum                                                                               						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUsuario   ณ Microsiga                                                                                					บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSetor     ณ Gestao do Leite                                                                         						บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ            			          	ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                   						บฑฑ
ฑฑฬออออออออออัออออออออออัออออออออออออออออออออออออออออออออออออออออออออออออออัออออออออออออออออออออออออออออออออออัอออออออออออออนฑฑ
ฑฑบAutor     ณ Data     ณ Motivo da Alteracao  				               ณUsuario(Filial+Matricula+Nome)    ณSetor        บฑฑ
ฑฑบฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤบฑฑ
ฑฑบ          ณ00/00/0000ณ                               				   ณ00-000000 -                       ณ TI	        บฑฑ
ฑฑบ--------- ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑบ          ณ          ณ                    							   ณ                                  ณ 			บฑฑ
ฑฑบ----------ณ----------ณ--------------------------------------------------ณ----------------------------------ณ-------------บฑฑ
ฑฑศออออออออออฯออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออฯออออออออออออออออออออออออออออออออออฯอออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/                

Static Function proxParc(_cCodAssoc,_cLojAssoc,_cNumTit,_cPrefixo)      

Local _cAlias  := GetNextAlias()   

Local _cRetorno:= "" 

/*
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณQuery para selecionar a maior parcela gerada					 ณ
//ณna SE2 para o titulo.                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
*/
querys(3,_cAlias,_cCodAssoc,_cLojAssoc,"",_cNumTit,_cPrefixo)

dbSelectArea(_cAlias)   
(_cAlias)->(dbGoTop())

_cRetorno:= Soma1((_cAlias)->MAXPARC)

dbSelectArea(_cAlias)   
(_cAlias)->(dbCloseArea())

While !MayIUseCode("Tr" + _cNumTit + xFilial("SE2") + _cRetorno)  //verifica se esta na memoria, sendo usado
		_cRetorno := Soma1(_cRetorno)						           // busca o proximo numero disponivel 
EndDo  

_cRetorno:= StrZero(Val(_cRetorno),TamSX3("E2_PARCELA")[1])

Return _cRetorno