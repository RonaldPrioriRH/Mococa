#INCLUDE "totvs.ch"
#INCLUDE "fileio.ch"
#INCLUDE "topconn.ch"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRFINR003  บAutor  ณTarcisio Silva MirandaบDataณ  21/02/2018 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Rela็ใo de funcionแrios. 								  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ 											                  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function RFINR003()

	// Declaracao de Variaveis
	Local 	aArea 		:= GetArea()
	Local	cQry        := ""
	Local 	cEol        := chr(13)+chr(10)
	Local 	dDataDe 	:= StoD("")
	Local 	dDataAte	:= StoD("")
	Local 	cMatDe 		:= ""
	Local 	cMatAte		:= ""	

	////////////////////////////////////////////////////////////////////////
	///Outras Variaveis
	////////////////////////////////////////////////////////////////////////
	Private cLocFile    := ""
	Private cPerg       := "RFINR003"

	////////////////////////////////////////////////////////////////////////
	///Cria as perguntas no SX1
	////////////////////////////////////////////////////////////////////////
	ValPer01()

	if !Pergunte(cPerg,.t.)
		return
	endif

	dDataDe		:= dTos(mv_par01)
	dDataAte	:= dTos(mv_par02)
	cMatDe 		:= mv_par03
	cMatAte  	:= mv_par04
	cLocFile	:= mv_par05

	cQry := " SELECT CASE WHEN NUMEMP = '1' THEN 'S.A' WHEN NUMEMP = '2' THEN 'ALIMENTOS' END AS EMPRESA,E1_FILIAL AS FILIAL, 		" + cEol 																							
	cQry += "      SE1.E1_PREFIXO AS PREFIXO, 																						" + cEol
	cQry += "      SE1.E1_NUM AS NUMERONF, 																							" + cEol
	cQry += "      SE1.E1_TIPO AS TIPO, 																							" + cEol
	cQry += "      SE1.E1_NATUREZ AS NATUREZA, 																						" + cEol
	cQry += "      SE1.E1_CLIENTE AS CLIENTE, 																						" + cEol
	cQry += "      SE1.E1_LOJA AS LOJA , 																							" + cEol
	cQry += "      SE1.E1_NOMCLI AS NOME, 																							" + cEol
	cQry += "      SUBSTR(SE1.E1_EMISSAO,7,2)||'/'||SUBSTR(SE1.E1_EMISSAO,5,2)||'/'||SUBSTR(SE1.E1_EMISSAO,1,4) AS EMISSAO,  		" + cEol
	cQry += "      SUBSTR(SE1.E1_VENCTO,7,2)||'/'||SUBSTR(SE1.E1_VENCTO,5,2)||'/'||SUBSTR(SE1.E1_VENCTO,1,4) AS VENCIMENTO,  		" + cEol
	cQry += "      SE1.E1_BAIXA AS DTBAIXA,																							" + cEol
	cQry += "      SE1.E1_VALOR AS VALOR,																							" + cEol
	cQry += "      FUN.NUMCAD AS MATRICULA																							" + cEol
	cQry += " FROM "+ RetSqlName("SE1") + " SE1, 	    																			" + cEol
	cQry += "       "+ RetSqlName("SA1") + " SA1   																				" + cEol
	cQry += "       VETPROD.R034FUN@WINTHOR2 FUN 																					" + cEol
	cQry += " WHERE  SA1.D_E_L_E_T_ = ' ' 																							" + cEol
	cQry += "       AND SE1.E1_EMISSAO BETWEEN '"+dDataDe+"' AND '"+dDataAte+"'														" + cEol
	
	if empty(cMatDe) .AND. cMatAte$"ZZZZZ"
		//cQry += "		AND FUN.NUMCAD BETWEEN '"+cMatDe+"' AND '"+cMatAte+"'														" + cEol
	else
		cQry += "		AND FUN.NUMCAD BETWEEN '"+cMatDe+"' AND '"+cMatAte+"'														" + cEol
	endif
	
	cQry += "       AND SE1.D_E_L_E_T_ = ' ' 																						" + cEol
	cQry += "       AND SE1.E1_CLIENTE = SA1.A1_COD 																				" + cEol
	cQry += "       AND SE1.E1_LOJA = SA1.A1_LOJA 																					" + cEol
	cQry += "       AND LPAD(TO_CHAR(FUN.NUMCPF), 11, '0') = TRIM(SA1.A1_CGC)														" + cEol
	cQry += "       AND SE1.E1_SALDO <> 0																							" + cEol
	cQry += "       AND TRIM(SE1.E1_BAIXA) IS NULL AND SITAFA = '1'   																" + cEol																								

	MemoWrite("C:\temp\RFINR003.sql",cQry)

	cQry := ChangeQuery(cQry)

	if Select("TRB") > 0
		TRB->(DbCloseArea())
	endif

	TcQuery cQry New Alias "TRB"

	dbSelectArea("TRB")
	TRB->(dbGoTop())


	Processa( {|| SPCPR01Excel() }, "Aguarde...", "Gerando planilha eletronica com as informa็๕es...",.F.)


	TRB->(DbCloseArea())
	RestArea(aArea)
Return

/*??????????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????????????
???????????????????????????????????????????????????????????????????????????????????
???Programa   ? ValPerg()  ? Autor ? Diego Bueno               ? Data ?20/03/2014???
????????????????????????????????????????????????????????????????????????????????J??
???Descricao  ? Cria perguntas no SX1	                                         ???
????????????????????????????????????????????????????????????????????????????????J??
???Observacao ?                                                                  ???
???           ?                                                                  ???
???????????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????????????
??????????????????????????????????????????????????????????????????????????????????*/

Static Function ValPer01()
	_sAlias	:=	Alias()
	//Retirado Sangelles 22/03/2022 ->dbSelectArea("SX1")
	//Retirado Sangelles 22/03/2022 ->dbSetOrder(1)
	cPerg 	:=	PADR(cPerg,10)

	U_xPutSx1(cPerg,"01","Emissใo de ?","."     	,"."       ,"mv_CH1","D",08,0,0,"G","","","","","MV_PAR01","","","","","","","","","","","","","","","","")
	U_xPutSx1(cPerg,"02","Emissใo At้ ?","."     	,"."       ,"mv_CH2","D",08,0,0,"G","","","","","MV_PAR02","","","","","","","","","","","","","","","","")
	U_xPutSx1(cPerg,"03","Matricula De ?","."     ,"."       ,"mv_CH3","C",05,0,0,"G","","","","","MV_PAR03","","","","","","","","","","","","","","","","")
	U_xPutSx1(cPerg,"04","Matricula At้ ?","."     ,"."       ,"mv_CH4","C",05,0,0,"G","","","","","MV_PAR04","","","","","","","","","","","","","","","","")
	U_xPutSx1(cPerg,"05","Salvar Em ?","."     ,"."       ,"mv_CH5","C",50,0,0,"G","","","","","MV_PAR05","","","","","","","","","","","","","","","","")

	//Retirado Sangelles 22/03/2022 ->dbSelectArea(_sAlias)

Return()

/*
?????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
???Programa  SPCPR01Excel   ?Autor ?Diego Bueno      ? Data ?  20/03/14   ???
????????????????????????????????????????????????????????????????????????????
???Desc.     ? Gera a impress?o em planilha eletronica.                   ???
???          ?                                                            ???
????????????????????????????????????????????????????????????????????????????
???Uso       ? Concebra                                                   ???
????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function SPCPR01Excel()
	Local oFWMSExcel := FWMSExcel():New()
	Local oMsExcel
	Local aCells
	Local cType
	Local cColumn
	Local cFile
	Local cFileTMP
	//Local cPicture
	Local lTotal
	Local nRow
	Local nRows
	Local nField
	Local nFields
	Local nAlign
	Local nFormat
	Local uCell
	Local _aHeadXls :={}
	Local _aColsXls :={}

	Local cWorkSheet := "Relat๓rio de Funcionแrio "
	Local cTable     := "Relat๓rio de Funcionแrio "+DtoC(Date())+" as "+Time()
	//Local lTotalize  := .F.
	//Local lPicture   := .F.

	dbSelectArea("TRB")
	TRB->(dbGoTop())

	ProcRegua(TRB->(Reccount()) )

	_aHeadXls := {"Filial","Empresa","Prefixo","Nota fiscal","Tipo","Natureza","Cliente","Loja","Nome","Dt.Emissใo","Dt.Vencimento","Dt.Baixa","Valor","Matricula"}

	While !TRB->(Eof())

		IncProc("Processando Relat๓rio: " + TRB->FILIAL + "-" + TRB->NUMERONF)

		aAdd(_aColsXls,{TRB->FILIAL,TRB->EMPRESA,TRB->PREFIXO,TRB->NUMERONF,TRB->TIPO,TRB->NATUREZA,TRB->CLIENTE,TRB->LOJA,TRB->NOME,TRB->EMISSAO,TRB->VENCIMENTO,TRB->DTBAIXA,TRB->VALOR,TRB->MATRICULA})

		TRB->(DbSkip())

	End

	If Len(_aColsXls) > 0

		BEGIN SEQUENCE

			oFWMSExcel:AddworkSheet(cWorkSheet)
			oFWMSExcel:AddTable(cWorkSheet,cTable)

			nFields := Len( _aHeadXls )
			For nField := 1 To nFields
				cType   := "C"
				cType := ValType(_aColsXls[1][nField])
				nAlign  := IF(cType=="C",1,IF(cType=="N",3,2))
				nFormat := IF(cType=="D",4,IF(cType=="N",2,1))
				cColumn := _aHeadXls[nField]
				lTotal  := .F. // ( lTotalize .and. cType == "N" )
				oFWMSExcel:AddColumn(@cWorkSheet,@cTable,@cColumn,@nAlign,@nFormat,@lTotal)
			Next nField

			oFWMSExcel:CBGCOLOR2LINE := '#FFFFFF'
			oFWMSExcel:CBGCOLORLINE  := '#FFFFFF'

			aCells := Array(nFields)
			nRows  := Len( _aColsXls )
			For nRow := 1 To nRows
				IncProc("Gerando planilha.. [Linha: "+TRANSFORM(nRow,"@E 999999")+" de "+TRANSFORM(Len(_aColsXls),"@E 999999")+" ]    ")
				For nField := 1 To nFields
					uCell := _aColsXls[nRow][nField]
					If Valtype(uCell) == "D" .AND. EMPTY(uCell)
						aCells[nField] := space(8)
					Else
						aCells[nField] := uCell
					Endif
				Next nField
				oFWMSExcel:AddRow(@cWorkSheet,@cTable,aClone(aCells))
			Next nRow
			oFWMSExcel:Activate()

			cFile := ( FwTemporaryTable( NIL, .F. ) + ".xls" )

			While File( cFile )
				cFile := ( FwTemporaryTable( NIL, .F. ) + ".xls" )
			End While

			oFWMSExcel:GetXMLFile( cFile )
			oFWMSExcel:DeActivate()

			IF .NOT.( File( cFile ) )
				MsgStop("Falha ao tentar criar arquivo "+cFile)
				cFile := ""
				BREAK
			EndIF

			cLocFile := AllTrim(cLocFile)
			if SubStr(cLocFile,Len(cLocFile),1) <> '\'
				cLocFile += '\'
			endif

			cFileTMP := ( cLocFile + cFile )
			IF .NOT.( __CopyFile( cFile , cFileTMP ) )
				MsgStop("Falha ao tentar salvar arquivo em "+cLocFile)
				fErase( cFile )
				cFile := ""
				BREAK
			EndIF

			fErase( cFile )

			cFile := cFileTMP

			IF .NOT.( File( cFile ) )
				MsgStop("Falha ao tentar salvar arquivo em "+cLocFile)
				cFile := ""
				BREAK
			EndIF

			IF .NOT.( ApOleClient("MsExcel") )
				MsgStop("Falha ao tentar abrir arquivo "+cFile+". Excel n?o instalado!")
				BREAK
			EndIF

			oMsExcel:= MsExcel():New()
			oMsExcel:WorkBooks:Open( cFile )
			oMsExcel:SetVisible( .T. )
			oMsExcel:= oMsExcel:Destroy()
		END SEQUENCE

		oFWMSExcel := FreeObj( oFWMSExcel )


	else
		MsgInfo("Nใo hแ dados para serem impressos!","VERIFIQUE OS PARAMETROS")
	endif

Return()
